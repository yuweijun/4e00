<!DOCTYPE html>

<HTML><head><TITLE>Manpage of PERLRETUT</TITLE>
<meta charset="utf-8">
<link rel="stylesheet" href="/css/main.css" type="text/css">
</head>
<body>
 <header class="site-header">
 <div class="wrap"> <div class="site-title"><a href="/manpages/index.html">linux manpages</a></div>
 <div class="site-description">{"type":"documentation"}</div>
 </div>
 </header>
 <div class="page-content"><div class="wrap">
<H1>PERLRETUT</H1>
Section: Perl Programmers Reference Guide (1)<BR>Updated: 2009-06-12<BR><A HREF="#index">Index</A>
<A HREF="/manpages/index.html">Return to Main Contents</A><HR>






<A NAME="lbAB">&nbsp;</A>
<H2>NAME</H2>

perlretut - Perl regular expressions tutorial
<A NAME="lbAC">&nbsp;</A>
<H2>DESCRIPTION</H2>

<A NAME="ixAAC"></A>
This page provides a basic tutorial on understanding, creating and
using regular expressions in Perl.  It serves as a complement to the
reference page on regular expressions perlre.  Regular expressions
are an integral part of the <TT>&quot;m//&quot;</TT>, <TT>&quot;s///&quot;</TT>, <TT>&quot;qr//&quot;</TT> and <TT>&quot;split&quot;</TT>
operators and so this tutorial also overlaps with
``Regexp Quote-Like Operators'' in perlop and ``split'' in perlfunc.
<P>

Perl is widely renowned for excellence in text processing, and regular
expressions are one of the big factors behind this fame.  Perl regular
expressions display an efficiency and flexibility unknown in most
other computer languages.  Mastering even the basics of regular
expressions will allow you to manipulate text with surprising ease.
<P>

What is a regular expression?  A regular expression is simply a string
that describes a pattern.  Patterns are in common use these days;
examples are the patterns typed into a search engine to find web pages
and the patterns used to list files in a directory, e.g., <TT>&quot;ls *.txt&quot;</TT>
or <TT>&quot;dir *.*&quot;</TT>.  In Perl, the patterns described by regular expressions
are used to search strings, extract desired parts of strings, and to
do search and replace operations.
<P>

Regular expressions have the undeserved reputation of being abstract
and difficult to understand.  Regular expressions are constructed using
simple concepts like conditionals and loops and are no more difficult
to understand than the corresponding <TT>&quot;if&quot;</TT> conditionals and <TT>&quot;while&quot;</TT>
loops in the Perl language itself.  In fact, the main challenge in
learning regular expressions is just getting used to the terse
notation used to express these concepts.
<P>

This tutorial flattens the learning curve by discussing regular
expression concepts, along with their notation, one at a time and with
many examples.  The first part of the tutorial will progress from the
simplest word searches to the basic regular expression concepts.  If
you master the first part, you will have all the tools needed to solve
about 98% of your needs.  The second part of the tutorial is for those
comfortable with the basics and hungry for more power tools.  It
discusses the more advanced regular expression operators and
introduces the latest cutting edge innovations in 5.6.0.
<P>

A note: to save time, 'regular expression' is often abbreviated as
regexp or regex.  Regexp is a more natural abbreviation than regex, but
is harder to pronounce.  The Perl pod documentation is evenly split on
regexp vs regex; in Perl, there is more than one way to abbreviate it.
We'll use regexp in this tutorial.
<A NAME="lbAD">&nbsp;</A>
<H2>Part 1: The basics</H2>

<A NAME="ixAAD"></A>
<A NAME="lbAE">&nbsp;</A>
<H3>Simple word matching</H3>

<A NAME="ixAAE"></A>
The simplest regexp is simply a word, or more generally, a string of
characters.  A regexp consisting of a word matches any string that
contains that word:
<P>



<PRE>
    &quot;Hello World&quot; =~ /World/;  # matches

</PRE>


<P>

What is this Perl statement all about? <TT>&quot;Hello World&quot;</TT> is a simple
double quoted string.  <TT>&quot;World&quot;</TT> is the regular expression and the
<TT>&quot;//&quot;</TT> enclosing <TT>&quot;/World/&quot;</TT> tells Perl to search a string for a match.
The operator <TT>&quot;=~&quot;</TT> associates the string with the regexp match and
produces a true value if the regexp matched, or false if the regexp
did not match.  In our case, <TT>&quot;World&quot;</TT> matches the second word in
<TT>&quot;Hello World&quot;</TT>, so the expression is true.  Expressions like this
are useful in conditionals:
<P>



<PRE>
    if (&quot;Hello World&quot; =~ /World/) {
        print &quot;It matches\n&quot;;
    }
    else {
        print &quot;It doesn't match\n&quot;;
    }

</PRE>


<P>

There are useful variations on this theme.  The sense of the match can
be reversed by using the <TT>&quot;!~&quot;</TT> operator:
<P>



<PRE>
    if (&quot;Hello World&quot; !~ /World/) {
        print &quot;It doesn't match\n&quot;;
    }
    else {
        print &quot;It matches\n&quot;;
    }

</PRE>


<P>

The literal string in the regexp can be replaced by a variable:
<P>



<PRE>
    $greeting = &quot;World&quot;;
    if (&quot;Hello World&quot; =~ /$greeting/) {
        print &quot;It matches\n&quot;;
    }
    else {
        print &quot;It doesn't match\n&quot;;
    }

</PRE>


<P>

If you're matching against the special default variable <TT>$_</TT>, the
<TT>&quot;$_ =~&quot;</TT> part can be omitted:
<P>



<PRE>
    $_ = &quot;Hello World&quot;;
    if (/World/) {
        print &quot;It matches\n&quot;;
    }
    else {
        print &quot;It doesn't match\n&quot;;
    }

</PRE>


<P>

And finally, the <TT>&quot;//&quot;</TT> default delimiters for a match can be changed
to arbitrary delimiters by putting an <TT>'m'</TT> out front:
<P>



<PRE>
    &quot;Hello World&quot; =~ m!World!;   # matches, delimited by '!'
    &quot;Hello World&quot; =~ m{World};   # matches, note the matching '{}'
    &quot;/usr/bin/perl&quot; =~ m&quot;/perl&quot;; # matches after '/usr/bin',
                                 # '/' becomes an ordinary char

</PRE>


<P>

<TT>&quot;/World/&quot;</TT>, <TT>&quot;m!World!&quot;</TT>, and <TT>&quot;m{World}&quot;</TT> all represent the
same thing.  When, e.g., the quote (<TT>&quot;&quot;&quot;</TT>) is used as a delimiter, the forward
slash <TT>'/'</TT> becomes an ordinary character and can be used in this regexp
without trouble.
<P>

Let's consider how different regexps would match <TT>&quot;Hello World&quot;</TT>:
<P>



<PRE>
    &quot;Hello World&quot; =~ /world/;  # doesn't match
    &quot;Hello World&quot; =~ /o W/;    # matches
    &quot;Hello World&quot; =~ /oW/;     # doesn't match
    &quot;Hello World&quot; =~ /World /; # doesn't match

</PRE>


<P>

The first regexp <TT>&quot;world&quot;</TT> doesn't match because regexps are
case-sensitive.  The second regexp matches because the substring
<TT>'o&nbsp;W'</TT> occurs in the string <TT>&quot;Hello&nbsp;World&quot;</TT>.  The space
character ' ' is treated like any other character in a regexp and is
needed to match in this case.  The lack of a space character is the
reason the third regexp <TT>'oW'</TT> doesn't match.  The fourth regexp
<TT>'World '</TT> doesn't match because there is a space at the end of the
regexp, but not at the end of the string.  The lesson here is that
regexps must match a part of the string <I>exactly</I> in order for the
statement to be true.
<P>

If a regexp matches in more than one place in the string, Perl will
always match at the earliest possible point in the string:
<P>



<PRE>
    &quot;Hello World&quot; =~ /o/;       # matches 'o' in 'Hello'
    &quot;That hat is red&quot; =~ /hat/; # matches 'hat' in 'That'

</PRE>


<P>

With respect to character matching, there are a few more points you
need to know about.   First of all, not all characters can be used 'as
is' in a match.  Some characters, called <I>metacharacters</I>, are reserved
for use in regexp notation.  The metacharacters are
<P>



<PRE>
    {}[]()^$.|*+?\

</PRE>


<P>

The significance of each of these will be explained
in the rest of the tutorial, but for now, it is important only to know
that a metacharacter can be matched by putting a backslash before it:
<P>



<PRE>
    &quot;2+2=4&quot; =~ /2+2/;    # doesn't match, + is a metacharacter
    &quot;2+2=4&quot; =~ /2\+2/;   # matches, \+ is treated like an ordinary +
    &quot;The interval is [0,1).&quot; =~ /[0,1)./     # is a syntax error!
    &quot;The interval is [0,1).&quot; =~ /\[0,1\)\./  # matches
    &quot;#!/usr/bin/perl&quot; =~ /#!\/usr\/bin\/perl/;  # matches

</PRE>


<P>

In the last regexp, the forward slash <TT>'/'</TT> is also backslashed,
because it is used to delimit the regexp.  This can lead to <FONT SIZE="-1">LTS</FONT>
(leaning toothpick syndrome), however, and it is often more readable
to change delimiters.
<P>



<PRE>
    &quot;#!/usr/bin/perl&quot; =~ m!#\!/usr/bin/perl!;  # easier to read

</PRE>


<P>

The backslash character <TT>'\'</TT> is a metacharacter itself and needs to
be backslashed:
<P>



<PRE>
    'C:\WIN32' =~ /C:\\WIN/;   # matches

</PRE>


<P>

In addition to the metacharacters, there are some <FONT SIZE="-1">ASCII</FONT> characters
which don't have printable character equivalents and are instead
represented by <I>escape sequences</I>.  Common examples are <TT>&quot;\t&quot;</TT> for a
tab, <TT>&quot;\n&quot;</TT> for a newline, <TT>&quot;\r&quot;</TT> for a carriage return and <TT>&quot;\a&quot;</TT> for a
bell.  If your string is better thought of as a sequence of arbitrary
bytes, the octal escape sequence, e.g., <TT>&quot;\033&quot;</TT>, or hexadecimal escape
sequence, e.g., <TT>&quot;\x1B&quot;</TT> may be a more natural representation for your
bytes.  Here are some examples of escapes:
<P>



<PRE>
    &quot;1000\t2000&quot; =~ m(0\t2)   # matches
    &quot;1000\n2000&quot; =~ /0\n20/   # matches
    &quot;1000\t2000&quot; =~ /\000\t2/ # doesn't match, &quot;0&quot; ne &quot;\000&quot;
    &quot;cat&quot;        =~ /\143\x61\x74/ # matches, but a weird way to spell cat

</PRE>


<P>

If you've been around Perl a while, all this talk of escape sequences
may seem familiar.  Similar escape sequences are used in double-quoted
strings and in fact the regexps in Perl are mostly treated as
double-quoted strings.  This means that variables can be used in
regexps as well.  Just like double-quoted strings, the values of the
variables in the regexp will be substituted in before the regexp is
evaluated for matching purposes.  So we have:
<P>



<PRE>
    $foo = 'house';
    'housecat' =~ /$foo/;      # matches
    'cathouse' =~ /cat$foo/;   # matches
    'housecat' =~ /${foo}cat/; # matches

</PRE>


<P>

So far, so good.  With the knowledge above you can already perform
searches with just about any literal string regexp you can dream up.
Here is a <I>very simple</I> emulation of the Unix grep program:
<P>



<PRE>
    % cat &gt; simple_grep
    #!/usr/bin/perl
    $regexp = shift;
    while (&lt;&gt;) {
        print if /$regexp/;
    }
    ^D

    % chmod +x simple_grep

    % simple_grep abba /usr/dict/words
    Babbage
    cabbage
    cabbages
    sabbath
    Sabbathize
    Sabbathizes
    sabbatical
    scabbard
    scabbards

</PRE>


<P>

This program is easy to understand.  <TT>&quot;#!/usr/bin/perl&quot;</TT> is the standard
way to invoke a perl program from the shell.
<TT>&quot;$regexp&nbsp;=&nbsp;shift;&quot;</TT> saves the first command line argument as the
regexp to be used, leaving the rest of the command line arguments to
be treated as files.  <TT>&quot;while&nbsp;(&lt;&gt;)&quot;</TT> loops over all the lines in
all the files.  For each line, <TT>&quot;print&nbsp;if&nbsp;/$regexp/;&quot;</TT> prints the
line if the regexp matches the line.  In this line, both <TT>&quot;print&quot;</TT> and
<TT>&quot;/$regexp/&quot;</TT> use the default variable <TT>$_</TT> implicitly.
<P>

With all of the regexps above, if the regexp matched anywhere in the
string, it was considered a match.  Sometimes, however, we'd like to
specify <I>where</I> in the string the regexp should try to match.  To do
this, we would use the <I>anchor</I> metacharacters <TT>&quot;^&quot;</TT> and <TT>&quot;$&quot;</TT>.  The
anchor <TT>&quot;^&quot;</TT> means match at the beginning of the string and the anchor
<TT>&quot;$&quot;</TT> means match at the end of the string, or before a newline at the
end of the string.  Here is how they are used:
<P>



<PRE>
    &quot;housekeeper&quot; =~ /keeper/;    # matches
    &quot;housekeeper&quot; =~ /^keeper/;   # doesn't match
    &quot;housekeeper&quot; =~ /keeper$/;   # matches
    &quot;housekeeper\n&quot; =~ /keeper$/; # matches

</PRE>


<P>

The second regexp doesn't match because <TT>&quot;^&quot;</TT> constrains <TT>&quot;keeper&quot;</TT> to
match only at the beginning of the string, but <TT>&quot;housekeeper&quot;</TT> has
keeper starting in the middle.  The third regexp does match, since the
<TT>&quot;$&quot;</TT> constrains <TT>&quot;keeper&quot;</TT> to match only at the end of the string.
<P>

When both <TT>&quot;^&quot;</TT> and <TT>&quot;$&quot;</TT> are used at the same time, the regexp has to
match both the beginning and the end of the string, i.e., the regexp
matches the whole string.  Consider
<P>



<PRE>
    &quot;keeper&quot; =~ /^keep$/;      # doesn't match
    &quot;keeper&quot; =~ /^keeper$/;    # matches
    &quot;&quot;       =~ /^$/;          # ^$ matches an empty string

</PRE>


<P>

The first regexp doesn't match because the string has more to it than
<TT>&quot;keep&quot;</TT>.  Since the second regexp is exactly the string, it
matches.  Using both <TT>&quot;^&quot;</TT> and <TT>&quot;$&quot;</TT> in a regexp forces the complete
string to match, so it gives you complete control over which strings
match and which don't.  Suppose you are looking for a fellow named
bert, off in a string by himself:
<P>



<PRE>
    &quot;dogbert&quot; =~ /bert/;   # matches, but not what you want

    &quot;dilbert&quot; =~ /^bert/;  # doesn't match, but ..
    &quot;bertram&quot; =~ /^bert/;  # matches, so still not good enough

    &quot;bertram&quot; =~ /^bert$/; # doesn't match, good
    &quot;dilbert&quot; =~ /^bert$/; # doesn't match, good
    &quot;bert&quot;    =~ /^bert$/; # matches, perfect

</PRE>


<P>

Of course, in the case of a literal string, one could just as easily
use the string comparison <TT>&quot;$string&nbsp;eq&nbsp;'bert'&quot;</TT> and it would be
more efficient.   The  <TT>&quot;^...$&quot;</TT> regexp really becomes useful when we
add in the more powerful regexp tools below.
<A NAME="lbAF">&nbsp;</A>
<H3>Using character classes</H3>

<A NAME="ixAAF"></A>
Although one can already do quite a lot with the literal string
regexps above, we've only scratched the surface of regular expression
technology.  In this and subsequent sections we will introduce regexp
concepts (and associated metacharacter notations) that will allow a
regexp to not just represent a single character sequence, but a <I>whole
class</I> of them.
<P>

One such concept is that of a <I>character class</I>.  A character class
allows a set of possible characters, rather than just a single
character, to match at a particular point in a regexp.  Character
classes are denoted by brackets <TT>&quot;[...]&quot;</TT>, with the set of characters
to be possibly matched inside.  Here are some examples:
<P>



<PRE>
    /cat/;       # matches 'cat'
    /[bcr]at/;   # matches 'bat, 'cat', or 'rat'
    /item[0123456789]/;  # matches 'item0' or ... or 'item9'
    &quot;abc&quot; =~ /[cab]/;    # matches 'a'

</PRE>


<P>

In the last statement, even though <TT>'c'</TT> is the first character in
the class, <TT>'a'</TT> matches because the first character position in the
string is the earliest point at which the regexp can match.
<P>



<PRE>
    /[yY][eE][sS]/;      # match 'yes' in a case-insensitive way
                         # 'yes', 'Yes', 'YES', etc.

</PRE>


<P>

This regexp displays a common task: perform a case-insensitive
match.  Perl provides a way of avoiding all those brackets by simply
appending an <TT>'i'</TT> to the end of the match.  Then <TT>&quot;/[yY][eE][sS]/;&quot;</TT>
can be rewritten as <TT>&quot;/yes/i;&quot;</TT>.  The <TT>'i'</TT> stands for
case-insensitive and is an example of a <I>modifier</I> of the matching
operation.  We will meet other modifiers later in the tutorial.
<P>

We saw in the section above that there were ordinary characters, which
represented themselves, and special characters, which needed a
backslash <TT>&quot;\&quot;</TT> to represent themselves.  The same is true in a
character class, but the sets of ordinary and special characters
inside a character class are different than those outside a character
class.  The special characters for a character class are <TT>&quot;-]\^$&quot;</TT> (and
the pattern delimiter, whatever it is).
<TT>&quot;]&quot;</TT> is special because it denotes the end of a character class.  <TT>&quot;$&quot;</TT> is
special because it denotes a scalar variable.  <TT>&quot;\&quot;</TT> is special because
it is used in escape sequences, just like above.  Here is how the
special characters <TT>&quot;]$\&quot;</TT> are handled:
<P>



<PRE>
   /[\]c]def/; # matches ']def' or 'cdef'
   $x = 'bcr';
   /[$x]at/;   # matches 'bat', 'cat', or 'rat'
   /[\$x]at/;  # matches '$at' or 'xat'
   /[\\$x]at/; # matches '\at', 'bat, 'cat', or 'rat'

</PRE>


<P>

The last two are a little tricky.  In <TT>&quot;[\$x]&quot;</TT>, the backslash protects
the dollar sign, so the character class has two members <TT>&quot;$&quot;</TT> and <TT>&quot;x&quot;</TT>.
In <TT>&quot;[\\$x]&quot;</TT>, the backslash is protected, so <TT>$x</TT> is treated as a
variable and substituted in double quote fashion.
<P>

The special character <TT>'-'</TT> acts as a range operator within character
classes, so that a contiguous set of characters can be written as a
range.  With ranges, the unwieldy <TT>&quot;[0123456789]&quot;</TT> and <TT>&quot;[abc...xyz]&quot;</TT>
become the svelte <TT>&quot;[0-9]&quot;</TT> and <TT>&quot;[a-z]&quot;</TT>.  Some examples are
<P>



<PRE>
    /item[0-9]/;  # matches 'item0' or ... or 'item9'
    /[0-9bx-z]aa/;  # matches '0aa', ..., '9aa',
                    # 'baa', 'xaa', 'yaa', or 'zaa'
    /[0-9a-fA-F]/;  # matches a hexadecimal digit
    /[0-9a-zA-Z_]/; # matches a &quot;word&quot; character,
                    # like those in a Perl variable name

</PRE>


<P>

If <TT>'-'</TT> is the first or last character in a character class, it is
treated as an ordinary character; <TT>&quot;[-ab]&quot;</TT>, <TT>&quot;[ab-]&quot;</TT> and <TT>&quot;[a\-b]&quot;</TT> are
all equivalent.
<P>

The special character <TT>&quot;^&quot;</TT> in the first position of a character class
denotes a <I>negated character class</I>, which matches any character but
those in the brackets.  Both <TT>&quot;[...]&quot;</TT> and <TT>&quot;[^...]&quot;</TT> must match a
character, or the match fails.  Then
<P>



<PRE>
    /[^a]at/;  # doesn't match 'aat' or 'at', but matches
               # all other 'bat', 'cat, '0at', '%at', etc.
    /[^0-9]/;  # matches a non-numeric character
    /[a^]at/;  # matches 'aat' or '^at'; here '^' is ordinary

</PRE>


<P>

Now, even <TT>&quot;[0-9]&quot;</TT> can be a bother to write multiple times, so in the
interest of saving keystrokes and making regexps more readable, Perl
has several abbreviations for common character classes, as shown below.
Since the introduction of Unicode, these character classes match more
than just a few characters in the <FONT SIZE="-1">ISO</FONT> 8859-1 range.
<DL COMPACT>
<DT>*<DD>
\d matches a digit, not just [0-9] but also digits from non-roman scripts
<DT>*<DD>
\s matches a whitespace character, the set [\ \t\r\n\f] and others
<DT>*<DD>
\w matches a word character (alphanumeric or _), not just [0-9a-zA-Z_]
but also digits and characters from non-roman scripts
<DT>*<DD>
\D is a negated \d; it represents any other character than a digit, or [^\d]
<DT>*<DD>
\S is a negated \s; it represents any non-whitespace character [^\s]
<DT>*<DD>
\W is a negated \w; it represents any non-word character [^\w]
<DT>*<DD>
The period '.' matches any character but ``\n'' (unless the modifier <TT>&quot;//s&quot;</TT> is
in effect, as explained below).
</DL>
<P>

The <TT>&quot;\d\s\w\D\S\W&quot;</TT> abbreviations can be used both inside and outside
of character classes.  Here are some in use:
<P>



<PRE>
    /\d\d:\d\d:\d\d/; # matches a hh:mm:ss time format
    /[\d\s]/;         # matches any digit or whitespace character
    /\w\W\w/;         # matches a word char, followed by a
                      # non-word char, followed by a word char
    /..rt/;           # matches any two chars, followed by 'rt'
    /end\./;          # matches 'end.'
    /end[.]/;         # same thing, matches 'end.'

</PRE>


<P>

Because a period is a metacharacter, it needs to be escaped to match
as an ordinary period. Because, for example, <TT>&quot;\d&quot;</TT> and <TT>&quot;\w&quot;</TT> are sets
of characters, it is incorrect to think of <TT>&quot;[^\d\w]&quot;</TT> as <TT>&quot;[\D\W]&quot;</TT>; in
fact <TT>&quot;[^\d\w]&quot;</TT> is the same as <TT>&quot;[^\w]&quot;</TT>, which is the same as
<TT>&quot;[\W]&quot;</TT>. Think DeMorgan's laws.
<P>

An anchor useful in basic regexps is the <I>word anchor</I>
<TT>&quot;\b&quot;</TT>.  This matches a boundary between a word character and a non-word
character <TT>&quot;\w\W&quot;</TT> or <TT>&quot;\W\w&quot;</TT>:
<P>



<PRE>
    $x = &quot;Housecat catenates house and cat&quot;;
    $x =~ /cat/;    # matches cat in 'housecat'
    $x =~ /\bcat/;  # matches cat in 'catenates'
    $x =~ /cat\b/;  # matches cat in 'housecat'
    $x =~ /\bcat\b/;  # matches 'cat' at end of string

</PRE>


<P>

Note in the last example, the end of the string is considered a word
boundary.
<P>

You might wonder why <TT>'.'</TT> matches everything but <TT>&quot;\n&quot;</TT> - why not
every character? The reason is that often one is matching against
lines and would like to ignore the newline characters.  For instance,
while the string <TT>&quot;\n&quot;</TT> represents one line, we would like to think
of it as empty.  Then
<P>



<PRE>
    &quot;&quot;   =~ /^$/;    # matches
    &quot;\n&quot; =~ /^$/;    # matches, $ anchors before &quot;\n&quot;

    &quot;&quot;   =~ /./;      # doesn't match; it needs a char
    &quot;&quot;   =~ /^.$/;    # doesn't match; it needs a char
    &quot;\n&quot; =~ /^.$/;    # doesn't match; it needs a char other than &quot;\n&quot;
    &quot;a&quot;  =~ /^.$/;    # matches
    &quot;a\n&quot;  =~ /^.$/;  # matches, $ anchors before &quot;\n&quot;

</PRE>


<P>

This behavior is convenient, because we usually want to ignore
newlines when we count and match characters in a line.  Sometimes,
however, we want to keep track of newlines.  We might even want <TT>&quot;^&quot;</TT>
and <TT>&quot;$&quot;</TT> to anchor at the beginning and end of lines within the
string, rather than just the beginning and end of the string.  Perl
allows us to choose between ignoring and paying attention to newlines
by using the <TT>&quot;//s&quot;</TT> and <TT>&quot;//m&quot;</TT> modifiers.  <TT>&quot;//s&quot;</TT> and <TT>&quot;//m&quot;</TT> stand for
single line and multi-line and they determine whether a string is to
be treated as one continuous string, or as a set of lines.  The two
modifiers affect two aspects of how the regexp is interpreted: 1) how
the <TT>'.'</TT> character class is defined, and 2) where the anchors <TT>&quot;^&quot;</TT>
and <TT>&quot;$&quot;</TT> are able to match.  Here are the four possible combinations:
<DL COMPACT>
<DT>*<DD>
no modifiers (//): Default behavior.  <TT>'.'</TT> matches any character
except <TT>&quot;\n&quot;</TT>.  <TT>&quot;^&quot;</TT> matches only at the beginning of the string and
<TT>&quot;$&quot;</TT> matches only at the end or before a newline at the end.
<DT>*<DD>
s modifier (//s): Treat string as a single long line.  <TT>'.'</TT> matches
any character, even <TT>&quot;\n&quot;</TT>.  <TT>&quot;^&quot;</TT> matches only at the beginning of
the string and <TT>&quot;$&quot;</TT> matches only at the end or before a newline at the
end.
<DT>*<DD>
m modifier (//m): Treat string as a set of multiple lines.  <TT>'.'</TT>
matches any character except <TT>&quot;\n&quot;</TT>.  <TT>&quot;^&quot;</TT> and <TT>&quot;$&quot;</TT> are able to match
at the start or end of <I>any</I> line within the string.
<DT>*<DD>
both s and m modifiers (//sm): Treat string as a single long line, but
detect multiple lines.  <TT>'.'</TT> matches any character, even
<TT>&quot;\n&quot;</TT>.  <TT>&quot;^&quot;</TT> and <TT>&quot;$&quot;</TT>, however, are able to match at the start or end
of <I>any</I> line within the string.
</DL>
<P>

Here are examples of <TT>&quot;//s&quot;</TT> and <TT>&quot;//m&quot;</TT> in action:
<P>



<PRE>
    $x = &quot;There once was a girl\nWho programmed in Perl\n&quot;;

    $x =~ /^Who/;   # doesn't match, &quot;Who&quot; not at start of string
    $x =~ /^Who/s;  # doesn't match, &quot;Who&quot; not at start of string
    $x =~ /^Who/m;  # matches, &quot;Who&quot; at start of second line
    $x =~ /^Who/sm; # matches, &quot;Who&quot; at start of second line

    $x =~ /girl.Who/;   # doesn't match, &quot;.&quot; doesn't match &quot;\n&quot;
    $x =~ /girl.Who/s;  # matches, &quot;.&quot; matches &quot;\n&quot;
    $x =~ /girl.Who/m;  # doesn't match, &quot;.&quot; doesn't match &quot;\n&quot;
    $x =~ /girl.Who/sm; # matches, &quot;.&quot; matches &quot;\n&quot;

</PRE>


<P>

Most of the time, the default behavior is what is wanted, but <TT>&quot;//s&quot;</TT> and
<TT>&quot;//m&quot;</TT> are occasionally very useful.  If <TT>&quot;//m&quot;</TT> is being used, the start
of the string can still be matched with <TT>&quot;\A&quot;</TT> and the end of the string
can still be matched with the anchors <TT>&quot;\Z&quot;</TT> (matches both the end and
the newline before, like <TT>&quot;$&quot;</TT>), and <TT>&quot;\z&quot;</TT> (matches only the end):
<P>



<PRE>
    $x =~ /^Who/m;   # matches, &quot;Who&quot; at start of second line
    $x =~ /\AWho/m;  # doesn't match, &quot;Who&quot; is not at start of string

    $x =~ /girl$/m;  # matches, &quot;girl&quot; at end of first line
    $x =~ /girl\Z/m; # doesn't match, &quot;girl&quot; is not at end of string

    $x =~ /Perl\Z/m; # matches, &quot;Perl&quot; is at newline before end
    $x =~ /Perl\z/m; # doesn't match, &quot;Perl&quot; is not at end of string

</PRE>


<P>

We now know how to create choices among classes of characters in a
regexp.  What about choices among words or character strings? Such
choices are described in the next section.
<A NAME="lbAG">&nbsp;</A>
<H3>Matching this or that</H3>

<A NAME="ixAAG"></A>
Sometimes we would like our regexp to be able to match different
possible words or character strings.  This is accomplished by using
the <I>alternation</I> metacharacter <TT>&quot;|&quot;</TT>.  To match <TT>&quot;dog&quot;</TT> or <TT>&quot;cat&quot;</TT>, we
form the regexp <TT>&quot;dog|cat&quot;</TT>.  As before, Perl will try to match the
regexp at the earliest possible point in the string.  At each
character position, Perl will first try to match the first
alternative, <TT>&quot;dog&quot;</TT>.  If <TT>&quot;dog&quot;</TT> doesn't match, Perl will then try the
next alternative, <TT>&quot;cat&quot;</TT>.  If <TT>&quot;cat&quot;</TT> doesn't match either, then the
match fails and Perl moves to the next position in the string.  Some
examples:
<P>



<PRE>
    &quot;cats and dogs&quot; =~ /cat|dog|bird/;  # matches &quot;cat&quot;
    &quot;cats and dogs&quot; =~ /dog|cat|bird/;  # matches &quot;cat&quot;

</PRE>


<P>

Even though <TT>&quot;dog&quot;</TT> is the first alternative in the second regexp,
<TT>&quot;cat&quot;</TT> is able to match earlier in the string.
<P>



<PRE>
    &quot;cats&quot;          =~ /c|ca|cat|cats/; # matches &quot;c&quot;
    &quot;cats&quot;          =~ /cats|cat|ca|c/; # matches &quot;cats&quot;

</PRE>


<P>

Here, all the alternatives match at the first string position, so the
first alternative is the one that matches.  If some of the
alternatives are truncations of the others, put the longest ones first
to give them a chance to match.
<P>



<PRE>
    &quot;cab&quot; =~ /a|b|c/ # matches &quot;c&quot;
                     # /a|b|c/ == /[abc]/

</PRE>


<P>

The last example points out that character classes are like
alternations of characters.  At a given character position, the first
alternative that allows the regexp match to succeed will be the one
that matches.
<A NAME="lbAH">&nbsp;</A>
<H3>Grouping things and hierarchical matching</H3>

<A NAME="ixAAH"></A>
Alternation allows a regexp to choose among alternatives, but by
itself it is unsatisfying.  The reason is that each alternative is a whole
regexp, but sometime we want alternatives for just part of a
regexp.  For instance, suppose we want to search for housecats or
housekeepers.  The regexp <TT>&quot;housecat|housekeeper&quot;</TT> fits the bill, but is
inefficient because we had to type <TT>&quot;house&quot;</TT> twice.  It would be nice to
have parts of the regexp be constant, like <TT>&quot;house&quot;</TT>, and some
parts have alternatives, like <TT>&quot;cat|keeper&quot;</TT>.
<P>

The <I>grouping</I> metacharacters <TT>&quot;()&quot;</TT> solve this problem.  Grouping
allows parts of a regexp to be treated as a single unit.  Parts of a
regexp are grouped by enclosing them in parentheses.  Thus we could solve
the <TT>&quot;housecat|housekeeper&quot;</TT> by forming the regexp as
<TT>&quot;house(cat|keeper)&quot;</TT>.  The regexp <TT>&quot;house(cat|keeper)&quot;</TT> means match
<TT>&quot;house&quot;</TT> followed by either <TT>&quot;cat&quot;</TT> or <TT>&quot;keeper&quot;</TT>.  Some more examples
are
<P>



<PRE>
    /(a|b)b/;    # matches 'ab' or 'bb'
    /(ac|b)b/;   # matches 'acb' or 'bb'
    /(^a|b)c/;   # matches 'ac' at start of string or 'bc' anywhere
    /(a|[bc])d/; # matches 'ad', 'bd', or 'cd'

    /house(cat|)/;  # matches either 'housecat' or 'house'
    /house(cat(s|)|)/;  # matches either 'housecats' or 'housecat' or
                        # 'house'.  Note groups can be nested.

    /(19|20|)\d\d/;  # match years 19xx, 20xx, or the Y2K problem, xx
    &quot;20&quot; =~ /(19|20|)\d\d/;  # matches the null alternative '()\d\d',
                             # because '20\d\d' can't match

</PRE>


<P>

Alternations behave the same way in groups as out of them: at a given
string position, the leftmost alternative that allows the regexp to
match is taken.  So in the last example at the first string position,
<TT>&quot;20&quot;</TT> matches the second alternative, but there is nothing left over
to match the next two digits <TT>&quot;\d\d&quot;</TT>.  So Perl moves on to the next
alternative, which is the null alternative and that works, since
<TT>&quot;20&quot;</TT> is two digits.
<P>

The process of trying one alternative, seeing if it matches, and
moving on to the next alternative, while going back in the string
from where the previous alternative was tried, if it doesn't, is called
<I>backtracking</I>.  The term 'backtracking' comes from the idea that
matching a regexp is like a walk in the woods.  Successfully matching
a regexp is like arriving at a destination.  There are many possible
trailheads, one for each string position, and each one is tried in
order, left to right.  From each trailhead there may be many paths,
some of which get you there, and some which are dead ends.  When you
walk along a trail and hit a dead end, you have to backtrack along the
trail to an earlier point to try another trail.  If you hit your
destination, you stop immediately and forget about trying all the
other trails.  You are persistent, and only if you have tried all the
trails from all the trailheads and not arrived at your destination, do
you declare failure.  To be concrete, here is a step-by-step analysis
of what Perl does when it tries to match the regexp
<P>



<PRE>
    &quot;abcde&quot; =~ /(abd|abc)(df|d|de)/;

</PRE>


<DL COMPACT>
<DT>0<DD>
Start with the first letter in the string 'a'.
<DT>1<DD>
<A NAME="ixAAI"></A>
Try the first alternative in the first group 'abd'.
<DT>2<DD>
<A NAME="ixAAJ"></A>
Match 'a' followed by 'b'. So far so good.
<DT>3<DD>
<A NAME="ixAAK"></A>
'd' in the regexp doesn't match 'c' in the string - a dead
end.  So backtrack two characters and pick the second alternative in
the first group 'abc'.
<DT>4<DD>
<A NAME="ixAAL"></A>
Match 'a' followed by 'b' followed by 'c'.  We are on a roll
and have satisfied the first group. Set <TT>$1</TT> to 'abc'.
<DT>5<DD>
<A NAME="ixAAM"></A>
Move on to the second group and pick the first alternative
'df'.
<DT>6<DD>
<A NAME="ixAAN"></A>
Match the 'd'.
<DT>7<DD>
<A NAME="ixAAO"></A>
'f' in the regexp doesn't match 'e' in the string, so a dead
end.  Backtrack one character and pick the second alternative in the
second group 'd'.
<DT>8<DD>
<A NAME="ixAAP"></A>
'd' matches. The second grouping is satisfied, so set <TT>$2</TT> to
'd'.
<DT>9<DD>
<A NAME="ixAAQ"></A>
We are at the end of the regexp, so we are done! We have
matched 'abcd' out of the string ``abcde''.
</DL>
<P>

There are a couple of things to note about this analysis.  First, the
third alternative in the second group 'de' also allows a match, but we
stopped before we got to it - at a given character position, leftmost
wins.  Second, we were able to get a match at the first character
position of the string 'a'.  If there were no matches at the first
position, Perl would move to the second character position 'b' and
attempt the match all over again.  Only when all possible paths at all
possible character positions have been exhausted does Perl give
up and declare <TT>&quot;$string&nbsp;=~&nbsp;/(abd|abc)(df|d|de)/;&quot;</TT> to be false.
<P>

Even with all this work, regexp matching happens remarkably fast.  To
speed things up, Perl compiles the regexp into a compact sequence of
opcodes that can often fit inside a processor cache.  When the code is
executed, these opcodes can then run at full throttle and search very
quickly.
<A NAME="lbAI">&nbsp;</A>
<H3>Extracting matches</H3>

<A NAME="ixAAR"></A>
The grouping metacharacters <TT>&quot;()&quot;</TT> also serve another completely
different function: they allow the extraction of the parts of a string
that matched.  This is very useful to find out what matched and for
text processing in general.  For each grouping, the part that matched
inside goes into the special variables <TT>$1</TT>, <TT>$2</TT>, etc.  They can be
used just as ordinary variables:
<P>



<PRE>
    # extract hours, minutes, seconds
    if ($time =~ /(\d\d):(\d\d):(\d\d)/) {    # match hh:mm:ss format
        $hours = $1;
        $minutes = $2;
        $seconds = $3;
    }

</PRE>


<P>

Now, we know that in scalar context,
<TT>&quot;$time&nbsp;=~&nbsp;/(\d\d):(\d\d):(\d\d)/&quot;</TT> returns a true or false
value.  In list context, however, it returns the list of matched values
<TT>&quot;($1,$2,$3)&quot;</TT>.  So we could write the code more compactly as
<P>



<PRE>
    # extract hours, minutes, seconds
    ($hours, $minutes, $second) = ($time =~ /(\d\d):(\d\d):(\d\d)/);

</PRE>


<P>

If the groupings in a regexp are nested, <TT>$1</TT> gets the group with the
leftmost opening parenthesis, <TT>$2</TT> the next opening parenthesis,
etc.  Here is a regexp with nested groups:
<P>



<PRE>
    /(ab(cd|ef)((gi)|j))/;
     1  2      34

</PRE>


<P>

If this regexp matches, <TT>$1</TT> contains a string starting with
<TT>'ab'</TT>, <TT>$2</TT> is either set to <TT>'cd'</TT> or <TT>'ef'</TT>, <TT>$3</TT> equals either
<TT>'gi'</TT> or <TT>'j'</TT>, and <TT>$4</TT> is either set to <TT>'gi'</TT>, just like <TT>$3</TT>,
or it remains undefined.
<P>

For convenience, Perl sets <TT>$+</TT> to the string held by the highest numbered
<TT>$1</TT>, <TT>$2</TT>,... that got assigned (and, somewhat related, <TT>$^N</TT> to the
value of the <TT>$1</TT>, <TT>$2</TT>,... most-recently assigned; i.e. the <TT>$1</TT>,
<TT>$2</TT>,... associated with the rightmost closing parenthesis used in the
match).
<A NAME="lbAJ">&nbsp;</A>
<H3>Backreferences</H3>

<A NAME="ixAAS"></A>
Closely associated with the matching variables <TT>$1</TT>, <TT>$2</TT>, ... are
the <I>backreferences</I> <TT>&quot;\1&quot;</TT>, <TT>&quot;\2&quot;</TT>,...  Backreferences are simply
matching variables that can be used <I>inside</I> a regexp.  This is a
really nice feature --- what matches later in a regexp is made to depend on
what matched earlier in the regexp.  Suppose we wanted to look
for doubled words in a text, like 'the the'.  The following regexp finds
all 3-letter doubles with a space in between:
<P>



<PRE>
    /\b(\w\w\w)\s\1\b/;

</PRE>


<P>

The grouping assigns a value to \1, so that the same 3 letter sequence
is used for both parts.
<P>

A similar task is to find words consisting of two identical parts:
<P>



<PRE>
    % simple_grep '^(\w\w\w\w|\w\w\w|\w\w|\w)\1$' /usr/dict/words
    beriberi
    booboo
    coco
    mama
    murmur
    papa

</PRE>


<P>

The regexp has a single grouping which considers 4-letter
combinations, then 3-letter combinations, etc., and uses <TT>&quot;\1&quot;</TT> to look for
a repeat.  Although <TT>$1</TT> and <TT>&quot;\1&quot;</TT> represent the same thing, care should be
taken to use matched variables <TT>$1</TT>, <TT>$2</TT>,... only <I>outside</I> a regexp
and backreferences <TT>&quot;\1&quot;</TT>, <TT>&quot;\2&quot;</TT>,... only <I>inside</I> a regexp; not doing
so may lead to surprising and unsatisfactory results.
<A NAME="lbAK">&nbsp;</A>
<H3>Relative backreferences</H3>

<A NAME="ixAAT"></A>
Counting the opening parentheses to get the correct number for a
backreference is errorprone as soon as there is more than one
capturing group.  A more convenient technique became available
with Perl 5.10: relative backreferences. To refer to the immediately
preceding capture group one now may write <TT>&quot;\g{-1}&quot;</TT>, the next but
last is available via <TT>&quot;\g{-2}&quot;</TT>, and so on.
<P>

Another good reason in addition to readability and maintainability
for using relative backreferences  is illustrated by the following example,
where a simple pattern for matching peculiar strings is used:
<P>



<PRE>
    $a99a = '([a-z])(\d)\2\1';   # matches a11a, g22g, x33x, etc.

</PRE>


<P>

Now that we have this pattern stored as a handy string, we might feel
tempted to use it as a part of some other pattern:
<P>



<PRE>
    $line = &quot;code=e99e&quot;;
    if ($line =~ /^(\w+)=$a99a$/){   # unexpected behavior!
        print &quot;$1 is valid\n&quot;;
    } else {
        print &quot;bad line: '$line'\n&quot;;
    }

</PRE>


<P>

But this doesn't match --- at least not the way one might expect. Only
after inserting the interpolated <TT>$a99a</TT> and looking at the resulting
full text of the regexp is it obvious that the backreferences have
backfired --- the subexpression <TT>&quot;(\w+)&quot;</TT> has snatched number 1 and
demoted the groups in <TT>$a99a</TT> by one rank. This can be avoided by
using relative backreferences:
<P>



<PRE>
    $a99a = '([a-z])(\d)\g{-1}\g{-2}';  # safe for being interpolated

</PRE>


<A NAME="lbAL">&nbsp;</A>
<H3>Named backreferences</H3>

<A NAME="ixAAU"></A>
Perl 5.10 also introduced named capture buffers and named backreferences.
To attach a name to a capturing group, you write either
<TT>&quot;(?&lt;name&gt;...)&quot;</TT> or <TT>&quot;(?'name'...)&quot;</TT>.  The backreference may
then be written as <TT>&quot;\g{name}&quot;</TT>.  It is permissible to attach the
same name to more than one group, but then only the leftmost one of the
eponymous set can be referenced.  Outside of the pattern a named
capture buffer is accessible through the <TT>&quot;%+&quot;</TT> hash.
<P>

Assuming that we have to match calendar dates which may be given in one
of the three formats yyyy-mm-dd, mm/dd/yyyy or dd.mm.yyyy, we can write
three suitable patterns where we use 'd', 'm' and 'y' respectively as the
names of the buffers capturing the pertaining components of a date. The
matching operation combines the three patterns as alternatives:
<P>



<PRE>
    $fmt1 = '(?&lt;y&gt;\d\d\d\d)-(?&lt;m&gt;\d\d)-(?&lt;d&gt;\d\d)';
    $fmt2 = '(?&lt;m&gt;\d\d)/(?&lt;d&gt;\d\d)/(?&lt;y&gt;\d\d\d\d)';
    $fmt3 = '(?&lt;d&gt;\d\d)\.(?&lt;m&gt;\d\d)\.(?&lt;y&gt;\d\d\d\d)';
    for my $d qw( 2006-10-21 15.01.2007 10/31/2005 ){
        if ( $d =~ m{$fmt1|$fmt2|$fmt3} ){
            print &quot;day=$+{d} month=$+{m} year=$+{y}\n&quot;;
        }
    }

</PRE>


<P>

If any of the alternatives matches, the hash <TT>&quot;%+&quot;</TT> is bound to contain the
three key-value pairs.
<A NAME="lbAM">&nbsp;</A>
<H3>Alternative capture group numbering</H3>

<A NAME="ixAAV"></A>
Yet another capturing group numbering technique (also as from Perl 5.10)
deals with the problem of referring to groups within a set of alternatives.
Consider a pattern for matching a time of the day, civil or military style:
<P>



<PRE>
    if ( $time =~ /(\d\d|\d):(\d\d)|(\d\d)(\d\d)/ ){
        # process hour and minute
    }

</PRE>


<P>

Processing the results requires an additional if statement to determine
whether <TT>$1</TT> and <TT>$2</TT> or <TT>$3</TT> and <TT>$4</TT> contain the goodies. It would
be easier if we could use buffer numbers 1 and 2 in second alternative as
well, and this is exactly what the parenthesized construct <TT>&quot;(?|...)&quot;</TT>,
set around an alternative achieves. Here is an extended version of the
previous pattern:
<P>



<PRE>
    if ( $time =~ /(?|(\d\d|\d):(\d\d)|(\d\d)(\d\d))\s+([A-Z][A-Z][A-Z])/ ){
        print &quot;hour=$1 minute=$2 zone=$3\n&quot;;
    }

</PRE>


<P>

Within the alternative numbering group, buffer numbers start at the same
position for each alternative. After the group, numbering continues
with one higher than the maximum reached across all the alternatives.
<A NAME="lbAN">&nbsp;</A>
<H3>Position information</H3>

<A NAME="ixAAW"></A>
In addition to what was matched, Perl (since 5.6.0) also provides the
positions of what was matched as contents of the <TT>&quot;@-&quot;</TT> and <TT>&quot;@+&quot;</TT>
arrays. <TT>&quot;$-[0]&quot;</TT> is the position of the start of the entire match and
<TT>$+[0]</TT> is the position of the end. Similarly, <TT>&quot;$-[n]&quot;</TT> is the
position of the start of the <TT>$n</TT> match and <TT>$+[n]</TT> is the position
of the end. If <TT>$n</TT> is undefined, so are <TT>&quot;$-[n]&quot;</TT> and <TT>$+[n]</TT>. Then
this code
<P>



<PRE>
    $x = &quot;Mmm...donut, thought Homer&quot;;
    $x =~ /^(Mmm|Yech)\.\.\.(donut|peas)/; # matches
    foreach $expr (1..$#-) {
        print &quot;Match $expr: '${$expr}' at position ($-[$expr],$+[$expr])\n&quot;;
    }

</PRE>


<P>

prints
<P>



<PRE>
    Match 1: 'Mmm' at position (0,3)
    Match 2: 'donut' at position (6,11)

</PRE>


<P>

Even if there are no groupings in a regexp, it is still possible to
find out what exactly matched in a string.  If you use them, Perl
will set <TT>&quot;$`&quot;</TT> to the part of the string before the match, will set <TT>$&amp;</TT>
to the part of the string that matched, and will set <TT>&quot;$'&quot;</TT> to the part
of the string after the match.  An example:
<P>



<PRE>
    $x = &quot;the cat caught the mouse&quot;;
    $x =~ /cat/;  # $` = 'the ', $&amp; = 'cat', $' = ' caught the mouse'
    $x =~ /the/;  # $` = '', $&amp; = 'the', $' = ' cat caught the mouse'

</PRE>


<P>

In the second match, <TT>&quot;$`&quot;</TT> equals <TT>''</TT> because the regexp matched at the
first character position in the string and stopped; it never saw the
second 'the'.  It is important to note that using <TT>&quot;$`&quot;</TT> and <TT>&quot;$'&quot;</TT>
slows down regexp matching quite a bit, while <TT>$&amp;</TT> slows it down to a
lesser extent, because if they are used in one regexp in a program,
they are generated for <I>all</I> regexps in the program.  So if raw
performance is a goal of your application, they should be avoided.
If you need to extract the corresponding substrings, use <TT>&quot;@-&quot;</TT> and
<TT>&quot;@+&quot;</TT> instead:
<P>



<PRE>
    $` is the same as substr( $x, 0, $-[0] )
    $&amp; is the same as substr( $x, $-[0], $+[0]-$-[0] )
    $' is the same as substr( $x, $+[0] )

</PRE>


<A NAME="lbAO">&nbsp;</A>
<H3>Non-capturing groupings</H3>

<A NAME="ixAAX"></A>
A group that is required to bundle a set of alternatives may or may not be
useful as a capturing group.  If it isn't, it just creates a superfluous
addition to the set of available capture buffer values, inside as well as
outside the regexp.  Non-capturing groupings, denoted by <TT>&quot;(?:regexp)&quot;</TT>,
still allow the regexp to be treated as a single unit, but don't establish
a capturing buffer at the same time.  Both capturing and non-capturing
groupings are allowed to co-exist in the same regexp.  Because there is
no extraction, non-capturing groupings are faster than capturing
groupings.  Non-capturing groupings are also handy for choosing exactly
which parts of a regexp are to be extracted to matching variables:
<P>



<PRE>
    # match a number, $1-$4 are set, but we only want $1
    /([+-]?\ *(\d+(\.\d*)?|\.\d+)([eE][+-]?\d+)?)/;

    # match a number faster , only $1 is set
    /([+-]?\ *(?:\d+(?:\.\d*)?|\.\d+)(?:[eE][+-]?\d+)?)/;

    # match a number, get $1 = whole number, $2 = exponent
    /([+-]?\ *(?:\d+(?:\.\d*)?|\.\d+)(?:[eE]([+-]?\d+))?)/;

</PRE>


<P>

Non-capturing groupings are also useful for removing nuisance
elements gathered from a split operation where parentheses are
required for some reason:
<P>



<PRE>
    $x = '12aba34ba5';
    @num = split /(a|b)+/, $x;    # @num = ('12','a','34','b','5')
    @num = split /(?:a|b)+/, $x;  # @num = ('12','34','5')

</PRE>


<A NAME="lbAP">&nbsp;</A>
<H3>Matching repetitions</H3>

<A NAME="ixAAY"></A>
The examples in the previous section display an annoying weakness.  We
were only matching 3-letter words, or chunks of words of 4 letters or
less.  We'd like to be able to match words or, more generally, strings
of any length, without writing out tedious alternatives like
<TT>&quot;\w\w\w\w|\w\w\w|\w\w|\w&quot;</TT>.
<P>

This is exactly the problem the <I>quantifier</I> metacharacters <TT>&quot;?&quot;</TT>,
<TT>&quot;*&quot;</TT>, <TT>&quot;+&quot;</TT>, and <TT>&quot;{}&quot;</TT> were created for.  They allow us to delimit the
number of repeats for a portion of a regexp we consider to be a
match.  Quantifiers are put immediately after the character, character
class, or grouping that we want to specify.  They have the following
meanings:
<DL COMPACT>
<DT>*<DD>
<TT>&quot;a?&quot;</TT> means: match 'a' 1 or 0 times
<DT>*<DD>
<TT>&quot;a*&quot;</TT> means: match 'a' 0 or more times, i.e., any number of times
<DT>*<DD>
<TT>&quot;a+&quot;</TT> means: match 'a' 1 or more times, i.e., at least once
<DT>*<DD>
<TT>&quot;a{n,m}&quot;</TT> means: match at least <TT>&quot;n&quot;</TT> times, but not more than <TT>&quot;m&quot;</TT>
times.
<DT>*<DD>
<TT>&quot;a{n,}&quot;</TT> means: match at least <TT>&quot;n&quot;</TT> or more times
<DT>*<DD>
<TT>&quot;a{n}&quot;</TT> means: match exactly <TT>&quot;n&quot;</TT> times
</DL>
<P>

Here are some examples:
<P>



<PRE>
    /[a-z]+\s+\d*/;  # match a lowercase word, at least one space, and
                     # any number of digits
    /(\w+)\s+\1/;    # match doubled words of arbitrary length
    /y(es)?/i;       # matches 'y', 'Y', or a case-insensitive 'yes'
    $year =~ /\d{2,4}/;  # make sure year is at least 2 but not more
                         # than 4 digits
    $year =~ /\d{4}|\d{2}/;    # better match; throw out 3 digit dates
    $year =~ /\d{2}(\d{2})?/;  # same thing written differently. However,
                               # this produces $1 and the other does not.

    % simple_grep '^(\w+)\1$' /usr/dict/words   # isn't this easier?
    beriberi
    booboo
    coco
    mama
    murmur
    papa

</PRE>


<P>

For all of these quantifiers, Perl will try to match as much of the
string as possible, while still allowing the regexp to succeed.  Thus
with <TT>&quot;/a?.../&quot;</TT>, Perl will first try to match the regexp with the <TT>&quot;a&quot;</TT>
present; if that fails, Perl will try to match the regexp without the
<TT>&quot;a&quot;</TT> present.  For the quantifier <TT>&quot;*&quot;</TT>, we get the following:
<P>



<PRE>
    $x = &quot;the cat in the hat&quot;;
    $x =~ /^(.*)(cat)(.*)$/; # matches,
                             # $1 = 'the '
                             # $2 = 'cat'
                             # $3 = ' in the hat'

</PRE>


<P>

Which is what we might expect, the match finds the only <TT>&quot;cat&quot;</TT> in the
string and locks onto it.  Consider, however, this regexp:
<P>



<PRE>
    $x =~ /^(.*)(at)(.*)$/; # matches,
                            # $1 = 'the cat in the h'
                            # $2 = 'at'
                            # $3 = ''   (0 characters match)

</PRE>


<P>

One might initially guess that Perl would find the <TT>&quot;at&quot;</TT> in <TT>&quot;cat&quot;</TT> and
stop there, but that wouldn't give the longest possible string to the
first quantifier <TT>&quot;.*&quot;</TT>.  Instead, the first quantifier <TT>&quot;.*&quot;</TT> grabs as
much of the string as possible while still having the regexp match.  In
this example, that means having the <TT>&quot;at&quot;</TT> sequence with the final <TT>&quot;at&quot;</TT>
in the string.  The other important principle illustrated here is that
when there are two or more elements in a regexp, the <I>leftmost</I>
quantifier, if there is one, gets to grab as much the string as
possible, leaving the rest of the regexp to fight over scraps.  Thus in
our example, the first quantifier <TT>&quot;.*&quot;</TT> grabs most of the string, while
the second quantifier <TT>&quot;.*&quot;</TT> gets the empty string.   Quantifiers that
grab as much of the string as possible are called <I>maximal match</I> or
<I>greedy</I> quantifiers.
<P>

When a regexp can match a string in several different ways, we can use
the principles above to predict which way the regexp will match:
<DL COMPACT>
<DT>*<DD>
Principle 0: Taken as a whole, any regexp will be matched at the
earliest possible position in the string.
<DT>*<DD>
Principle 1: In an alternation <TT>&quot;a|b|c...&quot;</TT>, the leftmost alternative
that allows a match for the whole regexp will be the one used.
<DT>*<DD>
Principle 2: The maximal matching quantifiers <TT>&quot;?&quot;</TT>, <TT>&quot;*&quot;</TT>, <TT>&quot;+&quot;</TT> and
<TT>&quot;{n,m}&quot;</TT> will in general match as much of the string as possible while
still allowing the whole regexp to match.
<DT>*<DD>
Principle 3: If there are two or more elements in a regexp, the
leftmost greedy quantifier, if any, will match as much of the string
as possible while still allowing the whole regexp to match.  The next
leftmost greedy quantifier, if any, will try to match as much of the
string remaining available to it as possible, while still allowing the
whole regexp to match.  And so on, until all the regexp elements are
satisfied.
</DL>
<P>

As we have seen above, Principle 0 overrides the others --- the regexp
will be matched as early as possible, with the other principles
determining how the regexp matches at that earliest character
position.
<P>

Here is an example of these principles in action:
<P>



<PRE>
    $x = &quot;The programming republic of Perl&quot;;
    $x =~ /^(.+)(e|r)(.*)$/;  # matches,
                              # $1 = 'The programming republic of Pe'
                              # $2 = 'r'
                              # $3 = 'l'

</PRE>


<P>

This regexp matches at the earliest string position, <TT>'T'</TT>.  One
might think that <TT>&quot;e&quot;</TT>, being leftmost in the alternation, would be
matched, but <TT>&quot;r&quot;</TT> produces the longest string in the first quantifier.
<P>



<PRE>
    $x =~ /(m{1,2})(.*)$/;  # matches,
                            # $1 = 'mm'
                            # $2 = 'ing republic of Perl'

</PRE>


<P>

Here, The earliest possible match is at the first <TT>'m'</TT> in
<TT>&quot;programming&quot;</TT>. <TT>&quot;m{1,2}&quot;</TT> is the first quantifier, so it gets to match
a maximal <TT>&quot;mm&quot;</TT>.
<P>



<PRE>
    $x =~ /.*(m{1,2})(.*)$/;  # matches,
                              # $1 = 'm'
                              # $2 = 'ing republic of Perl'

</PRE>


<P>

Here, the regexp matches at the start of the string. The first
quantifier <TT>&quot;.*&quot;</TT> grabs as much as possible, leaving just a single
<TT>'m'</TT> for the second quantifier <TT>&quot;m{1,2}&quot;</TT>.
<P>



<PRE>
    $x =~ /(.?)(m{1,2})(.*)$/;  # matches,
                                # $1 = 'a'
                                # $2 = 'mm'
                                # $3 = 'ing republic of Perl'

</PRE>


<P>

Here, <TT>&quot;.?&quot;</TT> eats its maximal one character at the earliest possible
position in the string, <TT>'a'</TT> in <TT>&quot;programming&quot;</TT>, leaving <TT>&quot;m{1,2}&quot;</TT>
the opportunity to match both <TT>&quot;m&quot;</TT>'s. Finally,
<P>



<PRE>
    &quot;aXXXb&quot; =~ /(X*)/; # matches with $1 = ''

</PRE>


<P>

because it can match zero copies of <TT>'X'</TT> at the beginning of the
string.  If you definitely want to match at least one <TT>'X'</TT>, use
<TT>&quot;X+&quot;</TT>, not <TT>&quot;X*&quot;</TT>.
<P>

Sometimes greed is not good.  At times, we would like quantifiers to
match a <I>minimal</I> piece of string, rather than a maximal piece.  For
this purpose, Larry Wall created the <I>minimal match</I> or
<I>non-greedy</I> quantifiers <TT>&quot;??&quot;</TT>, <TT>&quot;*?&quot;</TT>, <TT>&quot;+?&quot;</TT>, and <TT>&quot;{}?&quot;</TT>.  These are
the usual quantifiers with a <TT>&quot;?&quot;</TT> appended to them.  They have the
following meanings:
<DL COMPACT>
<DT>*<DD>
<TT>&quot;a??&quot;</TT> means: match 'a' 0 or 1 times. Try 0 first, then 1.
<DT>*<DD>
<TT>&quot;a*?&quot;</TT> means: match 'a' 0 or more times, i.e., any number of times,
but as few times as possible
<DT>*<DD>
<TT>&quot;a+?&quot;</TT> means: match 'a' 1 or more times, i.e., at least once, but
as few times as possible
<DT>*<DD>
<TT>&quot;a{n,m}?&quot;</TT> means: match at least <TT>&quot;n&quot;</TT> times, not more than <TT>&quot;m&quot;</TT>
times, as few times as possible
<DT>*<DD>
<TT>&quot;a{n,}?&quot;</TT> means: match at least <TT>&quot;n&quot;</TT> times, but as few times as
possible
<DT>*<DD>
<TT>&quot;a{n}?&quot;</TT> means: match exactly <TT>&quot;n&quot;</TT> times.  Because we match exactly
<TT>&quot;n&quot;</TT> times, <TT>&quot;a{n}?&quot;</TT> is equivalent to <TT>&quot;a{n}&quot;</TT> and is just there for
notational consistency.
</DL>
<P>

Let's look at the example above, but with minimal quantifiers:
<P>



<PRE>
    $x = &quot;The programming republic of Perl&quot;;
    $x =~ /^(.+?)(e|r)(.*)$/; # matches,
                              # $1 = 'Th'
                              # $2 = 'e'
                              # $3 = ' programming republic of Perl'

</PRE>


<P>

The minimal string that will allow both the start of the string <TT>&quot;^&quot;</TT>
and the alternation to match is <TT>&quot;Th&quot;</TT>, with the alternation <TT>&quot;e|r&quot;</TT>
matching <TT>&quot;e&quot;</TT>.  The second quantifier <TT>&quot;.*&quot;</TT> is free to gobble up the
rest of the string.
<P>



<PRE>
    $x =~ /(m{1,2}?)(.*?)$/;  # matches,
                              # $1 = 'm'
                              # $2 = 'ming republic of Perl'

</PRE>


<P>

The first string position that this regexp can match is at the first
<TT>'m'</TT> in <TT>&quot;programming&quot;</TT>. At this position, the minimal <TT>&quot;m{1,2}?&quot;</TT>
matches just one <TT>'m'</TT>.  Although the second quantifier <TT>&quot;.*?&quot;</TT> would
prefer to match no characters, it is constrained by the end-of-string
anchor <TT>&quot;$&quot;</TT> to match the rest of the string.
<P>



<PRE>
    $x =~ /(.*?)(m{1,2}?)(.*)$/;  # matches,
                                  # $1 = 'The progra'
                                  # $2 = 'm'
                                  # $3 = 'ming republic of Perl'

</PRE>


<P>

In this regexp, you might expect the first minimal quantifier <TT>&quot;.*?&quot;</TT>
to match the empty string, because it is not constrained by a <TT>&quot;^&quot;</TT>
anchor to match the beginning of the word.  Principle 0 applies here,
however.  Because it is possible for the whole regexp to match at the
start of the string, it <I>will</I> match at the start of the string.  Thus
the first quantifier has to match everything up to the first <TT>&quot;m&quot;</TT>.  The
second minimal quantifier matches just one <TT>&quot;m&quot;</TT> and the third
quantifier matches the rest of the string.
<P>



<PRE>
    $x =~ /(.??)(m{1,2})(.*)$/;  # matches,
                                 # $1 = 'a'
                                 # $2 = 'mm'
                                 # $3 = 'ing republic of Perl'

</PRE>


<P>

Just as in the previous regexp, the first quantifier <TT>&quot;.??&quot;</TT> can match
earliest at position <TT>'a'</TT>, so it does.  The second quantifier is
greedy, so it matches <TT>&quot;mm&quot;</TT>, and the third matches the rest of the
string.
<P>

We can modify principle 3 above to take into account non-greedy
quantifiers:
<DL COMPACT>
<DT>*<DD>
Principle 3: If there are two or more elements in a regexp, the
leftmost greedy (non-greedy) quantifier, if any, will match as much
(little) of the string as possible while still allowing the whole
regexp to match.  The next leftmost greedy (non-greedy) quantifier, if
any, will try to match as much (little) of the string remaining
available to it as possible, while still allowing the whole regexp to
match.  And so on, until all the regexp elements are satisfied.
</DL>
<P>

Just like alternation, quantifiers are also susceptible to
backtracking.  Here is a step-by-step analysis of the example
<P>



<PRE>
    $x = &quot;the cat in the hat&quot;;
    $x =~ /^(.*)(at)(.*)$/; # matches,
                            # $1 = 'the cat in the h'
                            # $2 = 'at'
                            # $3 = ''   (0 matches)

</PRE>


<DL COMPACT>
<DT>0<DD>
Start with the first letter in the string 't'.
<DT>1<DD>
<A NAME="ixAAZ"></A>
The first quantifier '.*' starts out by matching the whole
string 'the cat in the hat'.
<DT>2<DD>
<A NAME="ixABA"></A>
'a' in the regexp element 'at' doesn't match the end of the
string.  Backtrack one character.
<DT>3<DD>
<A NAME="ixABB"></A>
'a' in the regexp element 'at' still doesn't match the last
letter of the string 't', so backtrack one more character.
<DT>4<DD>
<A NAME="ixABC"></A>
Now we can match the 'a' and the 't'.
<DT>5<DD>
<A NAME="ixABD"></A>
Move on to the third element '.*'.  Since we are at the end of
the string and '.*' can match 0 times, assign it the empty string.
<DT>6<DD>
<A NAME="ixABE"></A>
We are done!
</DL>
<P>

Most of the time, all this moving forward and backtracking happens
quickly and searching is fast. There are some pathological regexps,
however, whose execution time exponentially grows with the size of the
string.  A typical structure that blows up in your face is of the form
<P>



<PRE>
    /(a|b+)*/;

</PRE>


<P>

The problem is the nested indeterminate quantifiers.  There are many
different ways of partitioning a string of length n between the <TT>&quot;+&quot;</TT>
and <TT>&quot;*&quot;</TT>: one repetition with <TT>&quot;b+&quot;</TT> of length n, two repetitions with
the first <TT>&quot;b+&quot;</TT> length k and the second with length n-k, m repetitions
whose bits add up to length n, etc.  In fact there are an exponential
number of ways to partition a string as a function of its length.  A
regexp may get lucky and match early in the process, but if there is
no match, Perl will try <I>every</I> possibility before giving up.  So be
careful with nested <TT>&quot;*&quot;</TT>'s, <TT>&quot;{n,m}&quot;</TT>'s, and <TT>&quot;+&quot;</TT>'s.  The book
<I>Mastering Regular Expressions</I> by Jeffrey Friedl gives a wonderful
discussion of this and other efficiency issues.
<A NAME="lbAQ">&nbsp;</A>
<H3>Possessive quantifiers</H3>

<A NAME="ixABF"></A>
Backtracking during the relentless search for a match may be a waste
of time, particularly when the match is bound to fail.  Consider
the simple pattern
<P>



<PRE>
    /^\w+\s+\w+$/; # a word, spaces, a word

</PRE>


<P>

Whenever this is applied to a string which doesn't quite meet the
pattern's expectations such as <TT>&quot;abc&nbsp;&nbsp;&quot;</TT> or <TT>&quot;abc&nbsp;&nbsp;def&nbsp;&quot;</TT>,
the regex engine will backtrack, approximately once for each character
in the string.  But we know that there is no way around taking <I>all</I>
of the initial word characters to match the first repetition, that <I>all</I>
spaces must be eaten by the middle part, and the same goes for the second
word.
<P>

With the introduction of the <I>possessive quantifiers</I> in Perl 5.10, we
have a way of instructing the regex engine not to backtrack, with the
usual quantifiers with a <TT>&quot;+&quot;</TT> appended to them.  This makes them greedy as
well as stingy; once they succeed they won't give anything back to permit
another solution. They have the following meanings:
<DL COMPACT>
<DT>*<DD>
<TT>&quot;a{n,m}+&quot;</TT> means: match at least <TT>&quot;n&quot;</TT> times, not more than <TT>&quot;m&quot;</TT> times,
as many times as possible, and don't give anything up. <TT>&quot;a?+&quot;</TT> is short
for <TT>&quot;a{0,1}+&quot;</TT>
<DT>*<DD>
<TT>&quot;a{n,}+&quot;</TT> means: match at least <TT>&quot;n&quot;</TT> times, but as many times as possible,
and don't give anything up. <TT>&quot;a*+&quot;</TT> is short for <TT>&quot;a{0,}+&quot;</TT> and <TT>&quot;a++&quot;</TT> is
short for <TT>&quot;a{1,}+&quot;</TT>.
<DT>*<DD>
<TT>&quot;a{n}+&quot;</TT> means: match exactly <TT>&quot;n&quot;</TT> times.  It is just there for
notational consistency.
</DL>
<P>

These possessive quantifiers represent a special case of a more general
concept, the <I>independent subexpression</I>, see below.
<P>

As an example where a possessive quantifier is suitable we consider
matching a quoted string, as it appears in several programming languages.
The backslash is used as an escape character that indicates that the
next character is to be taken literally, as another character for the
string.  Therefore, after the opening quote, we expect a (possibly
empty) sequence of alternatives: either some character except an
unescaped quote or backslash or an escaped character.
<P>



<PRE>
    /&quot;(?:[^&quot;\\]++|\\.)*+&quot;/;

</PRE>


<A NAME="lbAR">&nbsp;</A>
<H3>Building a regexp</H3>

<A NAME="ixABG"></A>
At this point, we have all the basic regexp concepts covered, so let's
give a more involved example of a regular expression.  We will build a
regexp that matches numbers.
<P>

The first task in building a regexp is to decide what we want to match
and what we want to exclude.  In our case, we want to match both
integers and floating point numbers and we want to reject any string
that isn't a number.
<P>

The next task is to break the problem down into smaller problems that
are easily converted into a regexp.
<P>

The simplest case is integers.  These consist of a sequence of digits,
with an optional sign in front.  The digits we can represent with
<TT>&quot;\d+&quot;</TT> and the sign can be matched with <TT>&quot;[+-]&quot;</TT>.  Thus the integer
regexp is
<P>



<PRE>
    /[+-]?\d+/;  # matches integers

</PRE>


<P>

A floating point number potentially has a sign, an integral part, a
decimal point, a fractional part, and an exponent.  One or more of these
parts is optional, so we need to check out the different
possibilities.  Floating point numbers which are in proper form include
123., 0.345, .34, -1e6, and 25.4E-72.  As with integers, the sign out
front is completely optional and can be matched by <TT>&quot;[+-]?&quot;</TT>.  We can
see that if there is no exponent, floating point numbers must have a
decimal point, otherwise they are integers.  We might be tempted to
model these with <TT>&quot;\d*\.\d*&quot;</TT>, but this would also match just a single
decimal point, which is not a number.  So the three cases of floating
point number without exponent are
<P>



<PRE>
   /[+-]?\d+\./;  # 1., 321., etc.
   /[+-]?\.\d+/;  # .1, .234, etc.
   /[+-]?\d+\.\d+/;  # 1.0, 30.56, etc.

</PRE>


<P>

These can be combined into a single regexp with a three-way alternation:
<P>



<PRE>
   /[+-]?(\d+\.\d+|\d+\.|\.\d+)/;  # floating point, no exponent

</PRE>


<P>

In this alternation, it is important to put <TT>'\d+\.\d+'</TT> before
<TT>'\d+\.'</TT>.  If <TT>'\d+\.'</TT> were first, the regexp would happily match that
and ignore the fractional part of the number.
<P>

Now consider floating point numbers with exponents.  The key
observation here is that <I>both</I> integers and numbers with decimal
points are allowed in front of an exponent.  Then exponents, like the
overall sign, are independent of whether we are matching numbers with
or without decimal points, and can be 'decoupled' from the
mantissa.  The overall form of the regexp now becomes clear:
<P>



<PRE>
    /^(optional sign)(integer | f.p. mantissa)(optional exponent)$/;

</PRE>


<P>

The exponent is an <TT>&quot;e&quot;</TT> or <TT>&quot;E&quot;</TT>, followed by an integer.  So the
exponent regexp is
<P>



<PRE>
   /[eE][+-]?\d+/;  # exponent

</PRE>


<P>

Putting all the parts together, we get a regexp that matches numbers:
<P>



<PRE>
   /^[+-]?(\d+\.\d+|\d+\.|\.\d+|\d+)([eE][+-]?\d+)?$/;  # Ta da!

</PRE>


<P>

Long regexps like this may impress your friends, but can be hard to
decipher.  In complex situations like this, the <TT>&quot;//x&quot;</TT> modifier for a
match is invaluable.  It allows one to put nearly arbitrary whitespace
and comments into a regexp without affecting their meaning.  Using it,
we can rewrite our 'extended' regexp in the more pleasing form
<P>



<PRE>
   /^
      [+-]?         # first, match an optional sign
      (             # then match integers or f.p. mantissas:
          \d+\.\d+  # mantissa of the form a.b
         |\d+\.     # mantissa of the form a.
         |\.\d+     # mantissa of the form .b
         |\d+       # integer of the form a
      )
      ([eE][+-]?\d+)?  # finally, optionally match an exponent
   $/x;

</PRE>


<P>

If whitespace is mostly irrelevant, how does one include space
characters in an extended regexp? The answer is to backslash it
<TT>'\&nbsp;'</TT> or put it in a character class <TT>&quot;[&nbsp;]&quot;</TT>.  The same thing
goes for pound signs, use <TT>&quot;\#&quot;</TT> or <TT>&quot;[#]&quot;</TT>.  For instance, Perl allows
a space between the sign and the mantissa or integer, and we could add
this to our regexp as follows:
<P>



<PRE>
   /^
      [+-]?\ *      # first, match an optional sign *and space*
      (             # then match integers or f.p. mantissas:
          \d+\.\d+  # mantissa of the form a.b
         |\d+\.     # mantissa of the form a.
         |\.\d+     # mantissa of the form .b
         |\d+       # integer of the form a
      )
      ([eE][+-]?\d+)?  # finally, optionally match an exponent
   $/x;

</PRE>


<P>

In this form, it is easier to see a way to simplify the
alternation.  Alternatives 1, 2, and 4 all start with <TT>&quot;\d+&quot;</TT>, so it
could be factored out:
<P>



<PRE>
   /^
      [+-]?\ *      # first, match an optional sign
      (             # then match integers or f.p. mantissas:
          \d+       # start out with a ...
          (
              \.\d* # mantissa of the form a.b or a.
          )?        # ? takes care of integers of the form a
         |\.\d+     # mantissa of the form .b
      )
      ([eE][+-]?\d+)?  # finally, optionally match an exponent
   $/x;

</PRE>


<P>

or written in the compact form,
<P>



<PRE>
    /^[+-]?\ *(\d+(\.\d*)?|\.\d+)([eE][+-]?\d+)?$/;

</PRE>


<P>

This is our final regexp.  To recap, we built a regexp by
<DL COMPACT>
<DT>*<DD>
specifying the task in detail,
<DT>*<DD>
breaking down the problem into smaller parts,
<DT>*<DD>
translating the small parts into regexps,
<DT>*<DD>
combining the regexps,
<DT>*<DD>
and optimizing the final combined regexp.
</DL>
<P>

These are also the typical steps involved in writing a computer
program.  This makes perfect sense, because regular expressions are
essentially programs written in a little computer language that specifies
patterns.
<A NAME="lbAS">&nbsp;</A>
<H3>Using regular expressions in Perl</H3>

<A NAME="ixABH"></A>
The last topic of Part 1 briefly covers how regexps are used in Perl
programs.  Where do they fit into Perl syntax?
<P>

We have already introduced the matching operator in its default
<TT>&quot;/regexp/&quot;</TT> and arbitrary delimiter <TT>&quot;m!regexp!&quot;</TT> forms.  We have used
the binding operator <TT>&quot;=~&quot;</TT> and its negation <TT>&quot;!~&quot;</TT> to test for string
matches.  Associated with the matching operator, we have discussed the
single line <TT>&quot;//s&quot;</TT>, multi-line <TT>&quot;//m&quot;</TT>, case-insensitive <TT>&quot;//i&quot;</TT> and
extended <TT>&quot;//x&quot;</TT> modifiers.  There are a few more things you might
want to know about matching operators.
<P>

<I>Optimizing pattern evaluation</I>
<A NAME="ixABI"></A>
<P>

We pointed out earlier that variables in regexps are substituted
before the regexp is evaluated:
<P>



<PRE>
    $pattern = 'Seuss';
    while (&lt;&gt;) {
        print if /$pattern/;
    }

</PRE>


<P>

This will print any lines containing the word <TT>&quot;Seuss&quot;</TT>.  It is not as
efficient as it could be, however, because Perl has to re-evaluate
(or compile) <TT>$pattern</TT> each time through the loop.  If <TT>$pattern</TT> won't be
changing over the lifetime of the script, we can add the <TT>&quot;//o&quot;</TT>
modifier, which directs Perl to only perform variable substitutions
once:
<P>



<PRE>
    #!/usr/bin/perl
    #    Improved simple_grep
    $regexp = shift;
    while (&lt;&gt;) {
        print if /$regexp/o;  # a good deal faster
    }

</PRE>


<P>

<I>Prohibiting substitution</I>
<A NAME="ixABJ"></A>
<P>

If you change <TT>$pattern</TT> after the first substitution happens, Perl
will ignore it.  If you don't want any substitutions at all, use the
special delimiter <TT>&quot;m''&quot;</TT>:
<P>



<PRE>
    @pattern = ('Seuss');
    while (&lt;&gt;) {
        print if m'@pattern';  # matches literal '@pattern', not 'Seuss'
    }

</PRE>


<P>

Similar to strings, <TT>&quot;m''&quot;</TT> acts like apostrophes on a regexp; all other
<TT>&quot;m&quot;</TT> delimiters act like quotes.  If the regexp evaluates to the empty string,
the regexp in the <I>last successful match</I> is used instead.  So we have
<P>



<PRE>
    &quot;dog&quot; =~ /d/;  # 'd' matches
    &quot;dogbert =~ //;  # this matches the 'd' regexp used before

</PRE>


<P>

<I>Global matching</I>
<A NAME="ixABK"></A>
<P>

The final two modifiers <TT>&quot;//g&quot;</TT> and <TT>&quot;//c&quot;</TT> concern multiple matches.
The modifier <TT>&quot;//g&quot;</TT> stands for global matching and allows the
matching operator to match within a string as many times as possible.
In scalar context, successive invocations against a string will have
`<TT>&quot;//g&quot;</TT> jump from match to match, keeping track of position in the
string as it goes along.  You can get or set the position with the
<TT>&quot;pos()&quot;</TT> function.
<P>

The use of <TT>&quot;//g&quot;</TT> is shown in the following example.  Suppose we have
a string that consists of words separated by spaces.  If we know how
many words there are in advance, we could extract the words using
groupings:
<P>



<PRE>
    $x = &quot;cat dog house&quot;; # 3 words
    $x =~ /^\s*(\w+)\s+(\w+)\s+(\w+)\s*$/; # matches,
                                           # $1 = 'cat'
                                           # $2 = 'dog'
                                           # $3 = 'house'

</PRE>


<P>

But what if we had an indeterminate number of words? This is the sort
of task <TT>&quot;//g&quot;</TT> was made for.  To extract all words, form the simple
regexp <TT>&quot;(\w+)&quot;</TT> and loop over all matches with <TT>&quot;/(\w+)/g&quot;</TT>:
<P>



<PRE>
    while ($x =~ /(\w+)/g) {
        print &quot;Word is $1, ends at position &quot;, pos $x, &quot;\n&quot;;
    }

</PRE>


<P>

prints
<P>



<PRE>
    Word is cat, ends at position 3
    Word is dog, ends at position 7
    Word is house, ends at position 13

</PRE>


<P>

A failed match or changing the target string resets the position.  If
you don't want the position reset after failure to match, add the
<TT>&quot;//c&quot;</TT>, as in <TT>&quot;/regexp/gc&quot;</TT>.  The current position in the string is
associated with the string, not the regexp.  This means that different
strings have different positions and their respective positions can be
set or read independently.
<P>

In list context, <TT>&quot;//g&quot;</TT> returns a list of matched groupings, or if
there are no groupings, a list of matches to the whole regexp.  So if
we wanted just the words, we could use
<P>



<PRE>
    @words = ($x =~ /(\w+)/g);  # matches,
                                # $word[0] = 'cat'
                                # $word[1] = 'dog'
                                # $word[2] = 'house'

</PRE>


<P>

Closely associated with the <TT>&quot;//g&quot;</TT> modifier is the <TT>&quot;\G&quot;</TT> anchor.  The
<TT>&quot;\G&quot;</TT> anchor matches at the point where the previous <TT>&quot;//g&quot;</TT> match left
off.  <TT>&quot;\G&quot;</TT> allows us to easily do context-sensitive matching:
<P>



<PRE>
    $metric = 1;  # use metric units
    ...
    $x = &lt;FILE&gt;;  # read in measurement
    $x =~ /^([+-]?\d+)\s*/g;  # get magnitude
    $weight = $1;
    if ($metric) { # error checking
        print &quot;Units error!&quot; unless $x =~ /\Gkg\./g;
    }
    else {
        print &quot;Units error!&quot; unless $x =~ /\Glbs\./g;
    }
    $x =~ /\G\s+(widget|sprocket)/g;  # continue processing

</PRE>


<P>

The combination of <TT>&quot;//g&quot;</TT> and <TT>&quot;\G&quot;</TT> allows us to process the string a
bit at a time and use arbitrary Perl logic to decide what to do next.
Currently, the <TT>&quot;\G&quot;</TT> anchor is only fully supported when used to anchor
to the start of the pattern.
<P>

<TT>&quot;\G&quot;</TT> is also invaluable in processing fixed length records with
regexps.  Suppose we have a snippet of coding region <FONT SIZE="-1">DNA</FONT>, encoded as
base pair letters <TT>&quot;ATCGTTGAAT...&quot;</TT> and we want to find all the stop
codons <TT>&quot;TGA&quot;</TT>.  In a coding region, codons are 3-letter sequences, so
we can think of the <FONT SIZE="-1">DNA</FONT> snippet as a sequence of 3-letter records.  The
naive regexp
<P>



<PRE>
    # expanded, this is &quot;ATC GTT GAA TGC AAA TGA CAT GAC&quot;
    $dna = &quot;ATCGTTGAATGCAAATGACATGAC&quot;;
    $dna =~ /TGA/;

</PRE>


<P>

doesn't work; it may match a <TT>&quot;TGA&quot;</TT>, but there is no guarantee that
the match is aligned with codon boundaries, e.g., the substring
<TT>&quot;GTT&nbsp;GAA&quot;</TT> gives a match.  A better solution is
<P>



<PRE>
    while ($dna =~ /(\w\w\w)*?TGA/g) {  # note the minimal *?
        print &quot;Got a TGA stop codon at position &quot;, pos $dna, &quot;\n&quot;;
    }

</PRE>


<P>

which prints
<P>



<PRE>
    Got a TGA stop codon at position 18
    Got a TGA stop codon at position 23

</PRE>


<P>

Position 18 is good, but position 23 is bogus.  What happened?
<P>

The answer is that our regexp works well until we get past the last
real match.  Then the regexp will fail to match a synchronized <TT>&quot;TGA&quot;</TT>
and start stepping ahead one character position at a time, not what we
want.  The solution is to use <TT>&quot;\G&quot;</TT> to anchor the match to the codon
alignment:
<P>



<PRE>
    while ($dna =~ /\G(\w\w\w)*?TGA/g) {
        print &quot;Got a TGA stop codon at position &quot;, pos $dna, &quot;\n&quot;;
    }

</PRE>


<P>

This prints
<P>



<PRE>
    Got a TGA stop codon at position 18

</PRE>


<P>

which is the correct answer.  This example illustrates that it is
important not only to match what is desired, but to reject what is not
desired.
<P>

<I>Search and replace</I>
<A NAME="ixABL"></A>
<P>

Regular expressions also play a big role in <I>search and replace</I>
operations in Perl.  Search and replace is accomplished with the
<TT>&quot;s///&quot;</TT> operator.  The general form is
<TT>&quot;s/regexp/replacement/modifiers&quot;</TT>, with everything we know about
regexps and modifiers applying in this case as well.  The
<TT>&quot;replacement&quot;</TT> is a Perl double quoted string that replaces in the
string whatever is matched with the <TT>&quot;regexp&quot;</TT>.  The operator <TT>&quot;=~&quot;</TT> is
also used here to associate a string with <TT>&quot;s///&quot;</TT>.  If matching
against <TT>$_</TT>, the <TT>&quot;$_&nbsp;=~&quot;</TT> can be dropped.  If there is a match,
<TT>&quot;s///&quot;</TT> returns the number of substitutions made, otherwise it returns
false.  Here are a few examples:
<P>



<PRE>
    $x = &quot;Time to feed the cat!&quot;;
    $x =~ s/cat/hacker/;   # $x contains &quot;Time to feed the hacker!&quot;
    if ($x =~ s/^(Time.*hacker)!$/$1 now!/) {
        $more_insistent = 1;
    }
    $y = &quot;'quoted words'&quot;;
    $y =~ s/^'(.*)'$/$1/;  # strip single quotes,
                           # $y contains &quot;quoted words&quot;

</PRE>


<P>

In the last example, the whole string was matched, but only the part
inside the single quotes was grouped.  With the <TT>&quot;s///&quot;</TT> operator, the
matched variables <TT>$1</TT>, <TT>$2</TT>, etc.  are immediately available for use
in the replacement expression, so we use <TT>$1</TT> to replace the quoted
string with just what was quoted.  With the global modifier, <TT>&quot;s///g&quot;</TT>
will search and replace all occurrences of the regexp in the string:
<P>



<PRE>
    $x = &quot;I batted 4 for 4&quot;;
    $x =~ s/4/four/;   # doesn't do it all:
                       # $x contains &quot;I batted four for 4&quot;
    $x = &quot;I batted 4 for 4&quot;;
    $x =~ s/4/four/g;  # does it all:
                       # $x contains &quot;I batted four for four&quot;

</PRE>


<P>

If you prefer 'regex' over 'regexp' in this tutorial, you could use
the following program to replace it:
<P>



<PRE>
    % cat &gt; simple_replace
    #!/usr/bin/perl
    $regexp = shift;
    $replacement = shift;
    while (&lt;&gt;) {
        s/$regexp/$replacement/go;
        print;
    }
    ^D

    % simple_replace regexp regex perlretut.pod

</PRE>


<P>

In <TT>&quot;simple_replace&quot;</TT> we used the <TT>&quot;s///g&quot;</TT> modifier to replace all
occurrences of the regexp on each line and the <TT>&quot;s///o&quot;</TT> modifier to
compile the regexp only once.  As with <TT>&quot;simple_grep&quot;</TT>, both the
<TT>&quot;print&quot;</TT> and the <TT>&quot;s/$regexp/$replacement/go&quot;</TT> use <TT>$_</TT> implicitly.
<P>

A modifier available specifically to search and replace is the
<TT>&quot;s///e&quot;</TT> evaluation modifier.  <TT>&quot;s///e&quot;</TT> wraps an <TT>&quot;eval{...}&quot;</TT> around
the replacement string and the evaluated result is substituted for the
matched substring.  <TT>&quot;s///e&quot;</TT> is useful if you need to do a bit of
computation in the process of replacing text.  This example counts
character frequencies in a line:
<P>



<PRE>
    $x = &quot;Bill the cat&quot;;
    $x =~ s/(.)/$chars{$1}++;$1/eg;  # final $1 replaces char with itself
    print &quot;frequency of '$_' is $chars{$_}\n&quot;
        foreach (sort {$chars{$b} &lt;=&gt; $chars{$a}} keys %chars);

</PRE>


<P>

This prints
<P>



<PRE>
    frequency of ' ' is 2
    frequency of 't' is 2
    frequency of 'l' is 2
    frequency of 'B' is 1
    frequency of 'c' is 1
    frequency of 'e' is 1
    frequency of 'h' is 1
    frequency of 'i' is 1
    frequency of 'a' is 1

</PRE>


<P>

As with the match <TT>&quot;m//&quot;</TT> operator, <TT>&quot;s///&quot;</TT> can use other delimiters,
such as <TT>&quot;s!!!&quot;</TT> and <TT>&quot;s{}{}&quot;</TT>, and even <TT>&quot;s{}//&quot;</TT>.  If single quotes are
used <TT>&quot;s'''&quot;</TT>, then the regexp and replacement are treated as single
quoted strings and there are no substitutions.  <TT>&quot;s///&quot;</TT> in list context
returns the same thing as in scalar context, i.e., the number of
matches.
<P>

<I>The split function</I>
<A NAME="ixABM"></A>
<P>

The <TT>&quot;split()&quot;</TT> function is another place where a regexp is used.
<TT>&quot;split /regexp/, string, limit&quot;</TT> separates the <TT>&quot;string&quot;</TT> operand into
a list of substrings and returns that list.  The regexp must be designed
to match whatever constitutes the separators for the desired substrings.
The <TT>&quot;limit&quot;</TT>, if present, constrains splitting into no more than <TT>&quot;limit&quot;</TT>
number of strings.  For example, to split a string into words, use
<P>



<PRE>
    $x = &quot;Calvin and Hobbes&quot;;
    @words = split /\s+/, $x;  # $word[0] = 'Calvin'
                               # $word[1] = 'and'
                               # $word[2] = 'Hobbes'

</PRE>


<P>

If the empty regexp <TT>&quot;//&quot;</TT> is used, the regexp always matches and
the string is split into individual characters.  If the regexp has
groupings, then the resulting list contains the matched substrings from the
groupings as well.  For instance,
<P>



<PRE>
    $x = &quot;/usr/bin/perl&quot;;
    @dirs = split m!/!, $x;  # $dirs[0] = ''
                             # $dirs[1] = 'usr'
                             # $dirs[2] = 'bin'
                             # $dirs[3] = 'perl'
    @parts = split m!(/)!, $x;  # $parts[0] = ''
                                # $parts[1] = '/'
                                # $parts[2] = 'usr'
                                # $parts[3] = '/'
                                # $parts[4] = 'bin'
                                # $parts[5] = '/'
                                # $parts[6] = 'perl'

</PRE>


<P>

Since the first character of <TT>$x</TT> matched the regexp, <TT>&quot;split&quot;</TT> prepended
an empty initial element to the list.
<P>

If you have read this far, congratulations! You now have all the basic
tools needed to use regular expressions to solve a wide range of text
processing problems.  If this is your first time through the tutorial,
why not stop here and play around with regexps a while...  Part&nbsp;2
concerns the more esoteric aspects of regular expressions and those
concepts certainly aren't needed right at the start.
<A NAME="lbAT">&nbsp;</A>
<H2>Part 2: Power tools</H2>

<A NAME="ixABN"></A>
<FONT SIZE="-1">OK</FONT>, you know the basics of regexps and you want to know more.  If
matching regular expressions is analogous to a walk in the woods, then
the tools discussed in Part 1 are analogous to topo maps and a
compass, basic tools we use all the time.  Most of the tools in part 2
are analogous to flare guns and satellite phones.  They aren't used
too often on a hike, but when we are stuck, they can be invaluable.
<P>

What follows are the more advanced, less used, or sometimes esoteric
capabilities of Perl regexps.  In Part 2, we will assume you are
comfortable with the basics and concentrate on the new features.
<A NAME="lbAU">&nbsp;</A>
<H3>More on characters, strings, and character classes</H3>

<A NAME="ixABO"></A>
There are a number of escape sequences and character classes that we
haven't covered yet.
<P>

There are several escape sequences that convert characters or strings
between upper and lower case, and they are also available within
patterns.  <TT>&quot;\l&quot;</TT> and <TT>&quot;\u&quot;</TT> convert the next character to lower or
upper case, respectively:
<P>



<PRE>
    $x = &quot;perl&quot;;
    $string =~ /\u$x/;  # matches 'Perl' in $string
    $x = &quot;M(rs?|s)\\.&quot;; # note the double backslash
    $string =~ /\l$x/;  # matches 'mr.', 'mrs.', and 'ms.',

</PRE>


<P>

A <TT>&quot;\L&quot;</TT> or <TT>&quot;\U&quot;</TT> indicates a lasting conversion of case, until
terminated by <TT>&quot;\E&quot;</TT> or thrown over by another <TT>&quot;\U&quot;</TT> or <TT>&quot;\L&quot;</TT>:
<P>



<PRE>
    $x = &quot;This word is in lower case:\L SHOUT\E&quot;;
    $x =~ /shout/;       # matches
    $x = &quot;I STILL KEYPUNCH CARDS FOR MY 360&quot;
    $x =~ /\Ukeypunch/;  # matches punch card string

</PRE>


<P>

If there is no <TT>&quot;\E&quot;</TT>, case is converted until the end of the
string. The regexps <TT>&quot;\L\u$word&quot;</TT> or <TT>&quot;\u\L$word&quot;</TT> convert the first
character of <TT>$word</TT> to uppercase and the rest of the characters to
lowercase.
<P>

Control characters can be escaped with <TT>&quot;\c&quot;</TT>, so that a control-Z
character would be matched with <TT>&quot;\cZ&quot;</TT>.  The escape sequence
<TT>&quot;\Q&quot;</TT>...<TT>&quot;\E&quot;</TT> quotes, or protects most non-alphabetic characters.   For
instance,
<P>



<PRE>
    $x = &quot;\QThat !^*&amp;%~&amp; cat!&quot;;
    $x =~ /\Q!^*&amp;%~&amp;\E/;  # check for rough language

</PRE>


<P>

It does not protect <TT>&quot;$&quot;</TT> or <TT>&quot;@&quot;</TT>, so that variables can still be
substituted.
<P>

With the advent of 5.6.0, Perl regexps can handle more than just the
standard <FONT SIZE="-1">ASCII</FONT> character set.  Perl now supports <I>Unicode</I>, a standard
for representing the alphabets from virtually all of the world's written
languages, and a host of symbols.  Perl's text strings are Unicode strings, so
they can contain characters with a value (codepoint or character number) higher
than 255
<P>

What does this mean for regexps? Well, regexp users don't need to know
much about Perl's internal representation of strings.  But they do need
to know 1) how to represent Unicode characters in a regexp and 2) that
a matching operation will treat the string to be searched as a sequence
of characters, not bytes.  The answer to 1) is that Unicode characters
greater than <TT>&quot;chr(255)&quot;</TT> are represented using the <TT>&quot;\x{hex}&quot;</TT> notation,
because the \0 octal and \x hex (without curly braces) don't go further
than 255.
<P>



<PRE>
    /\x{263a}/;  # match a Unicode smiley face :)

</PRE>


<P>

<B></B><FONT SIZE="-1"><B>NOTE</B></FONT><B></B>: In Perl 5.6.0 it used to be that one needed to say <TT>&quot;use
utf8&quot;</TT> to use any Unicode features.  This is no more the case: for
almost all Unicode processing, the explicit <TT>&quot;utf8&quot;</TT> pragma is not
needed.  (The only case where it matters is if your Perl script is in
Unicode and encoded in <FONT SIZE="-1">UTF-8</FONT>, then an explicit <TT>&quot;use utf8&quot;</TT> is needed.)
<P>

Figuring out the hexadecimal sequence of a Unicode character you want
or deciphering someone else's hexadecimal Unicode regexp is about as
much fun as programming in machine code.  So another way to specify
Unicode characters is to use the <I>named character</I>&gt; escape
sequence <TT>&quot;\N{name}&quot;</TT>.  <TT>&quot;name&quot;</TT> is a name for the Unicode character, as
specified in the Unicode standard.  For instance, if we wanted to
represent or match the astrological sign for the planet Mercury, we
could use
<P>



<PRE>
    use charnames &quot;:full&quot;; # use named chars with Unicode full names
    $x = &quot;abc\N{MERCURY}def&quot;;
    $x =~ /\N{MERCURY}/;   # matches

</PRE>


<P>

One can also use short names or restrict names to a certain alphabet:
<P>



<PRE>
    use charnames ':full';
    print &quot;\N{GREEK SMALL LETTER SIGMA} is called sigma.\n&quot;;

    use charnames &quot;:short&quot;;
    print &quot;\N{greek:Sigma} is an upper-case sigma.\n&quot;;

    use charnames qw(greek);
    print &quot;\N{sigma} is Greek sigma\n&quot;;

</PRE>


<P>

A list of full names is found in the file NamesList.txt in the
lib/perl5/X.X.X/unicore directory (where X.X.X is the perl
version number as it is installed on your system).
<P>

The answer to requirement 2), as of 5.6.0, is that a regexp uses Unicode
characters. Internally, this is encoded to bytes using either <FONT SIZE="-1">UTF-8</FONT> or a
native 8 bit encoding, depending on the history of the string, but
conceptually it is a sequence of characters, not bytes. See
perlunitut for a tutorial about that.
<P>

Let us now discuss Unicode character classes.  Just as with Unicode
characters, there are named Unicode character classes represented by the
<TT>&quot;\p{name}&quot;</TT> escape sequence.  Closely associated is the <TT>&quot;\P{name}&quot;</TT>
character class, which is the negation of the <TT>&quot;\p{name}&quot;</TT> class.  For
example, to match lower and uppercase characters,
<P>



<PRE>
    use charnames &quot;:full&quot;; # use named chars with Unicode full names
    $x = &quot;BOB&quot;;
    $x =~ /^\p{IsUpper}/;   # matches, uppercase char class
    $x =~ /^\P{IsUpper}/;   # doesn't match, char class sans uppercase
    $x =~ /^\p{IsLower}/;   # doesn't match, lowercase char class
    $x =~ /^\P{IsLower}/;   # matches, char class sans lowercase

</PRE>


<P>

Here is the association between some Perl named classes and the
traditional Unicode classes:
<P>



<PRE>
    Perl class name  Unicode class name or regular expression

    IsAlpha          /^[LM]/
    IsAlnum          /^[LMN]/
    IsASCII          $code &lt;= 127
    IsCntrl          /^C/
    IsBlank          $code =~ /^(0020|0009)$/ || /^Z[^lp]/
    IsDigit          Nd
    IsGraph          /^([LMNPS]|Co)/
    IsLower          Ll
    IsPrint          /^([LMNPS]|Co|Zs)/
    IsPunct          /^P/
    IsSpace          /^Z/ || ($code =~ /^(0009|000A|000B|000C|000D)$/
    IsSpacePerl      /^Z/ || ($code =~ /^(0009|000A|000C|000D|0085|2028|2029)$/
    IsUpper          /^L[ut]/
    IsWord           /^[LMN]/ || $code eq &quot;005F&quot;
    IsXDigit         $code =~ /^00(3[0-9]|[46][1-6])$/

</PRE>


<P>

You can also use the official Unicode class names with the <TT>&quot;\p&quot;</TT> and
<TT>&quot;\P&quot;</TT>, like <TT>&quot;\p{L}&quot;</TT> for Unicode 'letters', or <TT>&quot;\p{Lu}&quot;</TT> for uppercase
letters, or <TT>&quot;\P{Nd}&quot;</TT> for non-digits.  If a <TT>&quot;name&quot;</TT> is just one
letter, the braces can be dropped.  For instance, <TT>&quot;\pM&quot;</TT> is the
character class of Unicode 'marks', for example accent marks.
For the full list see perlunicode.
<P>

The Unicode has also been separated into various sets of characters
which you can test with <TT>&quot;\p{...}&quot;</TT> (in) and <TT>&quot;\P{...}&quot;</TT> (not in).
To test whether a character is (or is not) an element of a script
you would use the script name, for example <TT>&quot;\p{Latin}&quot;</TT>, <TT>&quot;\p{Greek}&quot;</TT>,
or <TT>&quot;\P{Katakana}&quot;</TT>. Other sets are the Unicode blocks, the names
of which begin with ``In''. One such block is dedicated to mathematical
operators, and its pattern formula is &lt;C\p{InMathematicalOperators&gt;}&gt;.
For the full list see perlunicode.
<P>

<TT>&quot;\X&quot;</TT> is an abbreviation for a character class that comprises
the Unicode <I>combining character sequences</I>.  A combining character
sequence is a base character followed by any number of diacritics, i.e.,
signs like accents used to indicate different sounds of a letter. Using
the Unicode full names, e.g., <TT>&quot;A&nbsp;+&nbsp;COMBINING&nbsp;RING&quot;</TT> is a combining
character sequence with base character <TT>&quot;A&quot;</TT> and combining character
<TT>&quot;COMBINING&nbsp;RING&quot;</TT>, which translates in Danish to A with the circle
atop it, as in the word Angstrom.  <TT>&quot;\X&quot;</TT> is equivalent to <TT>&quot;\PM\pM*}&quot;</TT>,
i.e., a non-mark followed by one or more marks.
<P>

For the full and latest information about Unicode see the latest
Unicode standard, or the Unicode Consortium's website <A HREF="http://www.unicode.org/">http://www.unicode.org/</A>
<P>

As if all those classes weren't enough, Perl also defines <FONT SIZE="-1">POSIX</FONT> style
character classes.  These have the form <TT>&quot;[:name:]&quot;</TT>, with <TT>&quot;name&quot;</TT> the
name of the <FONT SIZE="-1">POSIX</FONT> class.  The <FONT SIZE="-1">POSIX</FONT> classes are <TT>&quot;alpha&quot;</TT>, <TT>&quot;alnum&quot;</TT>,
<TT>&quot;ascii&quot;</TT>, <TT>&quot;cntrl&quot;</TT>, <TT>&quot;digit&quot;</TT>, <TT>&quot;graph&quot;</TT>, <TT>&quot;lower&quot;</TT>, <TT>&quot;print&quot;</TT>, <TT>&quot;punct&quot;</TT>,
<TT>&quot;space&quot;</TT>, <TT>&quot;upper&quot;</TT>, and <TT>&quot;xdigit&quot;</TT>, and two extensions, <TT>&quot;word&quot;</TT> (a Perl
extension to match <TT>&quot;\w&quot;</TT>), and <TT>&quot;blank&quot;</TT> (a <FONT SIZE="-1">GNU</FONT> extension).  If <TT>&quot;utf8&quot;</TT>
is being used, then these classes are defined the same as their
corresponding Perl Unicode classes: <TT>&quot;[:upper:]&quot;</TT> is the same as
<TT>&quot;\p{IsUpper}&quot;</TT>, etc.  The <FONT SIZE="-1">POSIX</FONT> character classes, however, don't
require using <TT>&quot;utf8&quot;</TT>.  The <TT>&quot;[:digit:]&quot;</TT>, <TT>&quot;[:word:]&quot;</TT>, and
<TT>&quot;[:space:]&quot;</TT> correspond to the familiar <TT>&quot;\d&quot;</TT>, <TT>&quot;\w&quot;</TT>, and <TT>&quot;\s&quot;</TT>
character classes.  To negate a <FONT SIZE="-1">POSIX</FONT> class, put a <TT>&quot;^&quot;</TT> in front of
the name, so that, e.g., <TT>&quot;[:^digit:]&quot;</TT> corresponds to <TT>&quot;\D&quot;</TT> and under
<TT>&quot;utf8&quot;</TT>, <TT>&quot;\P{IsDigit}&quot;</TT>.  The Unicode and <FONT SIZE="-1">POSIX</FONT> character classes can
be used just like <TT>&quot;\d&quot;</TT>, with the exception that <FONT SIZE="-1">POSIX</FONT> character
classes can only be used inside of a character class:
<P>



<PRE>
    /\s+[abc[:digit:]xyz]\s*/;  # match a,b,c,x,y,z, or a digit
    /^=item\s[[:digit:]]/;      # match '=item',
                                # followed by a space and a digit
    use charnames &quot;:full&quot;;
    /\s+[abc\p{IsDigit}xyz]\s+/;  # match a,b,c,x,y,z, or a digit
    /^=item\s\p{IsDigit}/;        # match '=item',
                                  # followed by a space and a digit

</PRE>


<P>

Whew! That is all the rest of the characters and character classes.
<A NAME="lbAV">&nbsp;</A>
<H3>Compiling and saving regular expressions</H3>

<A NAME="ixABP"></A>
In Part 1 we discussed the <TT>&quot;//o&quot;</TT> modifier, which compiles a regexp
just once.  This suggests that a compiled regexp is some data structure
that can be stored once and used again and again.  The regexp quote
<TT>&quot;qr//&quot;</TT> does exactly that: <TT>&quot;qr/string/&quot;</TT> compiles the <TT>&quot;string&quot;</TT> as a
regexp and transforms the result into a form that can be assigned to a
variable:
<P>



<PRE>
    $reg = qr/foo+bar?/;  # reg contains a compiled regexp

</PRE>


<P>

Then <TT>$reg</TT> can be used as a regexp:
<P>



<PRE>
    $x = &quot;fooooba&quot;;
    $x =~ $reg;     # matches, just like /foo+bar?/
    $x =~ /$reg/;   # same thing, alternate form

</PRE>


<P>

<TT>$reg</TT> can also be interpolated into a larger regexp:
<P>



<PRE>
    $x =~ /(abc)?$reg/;  # still matches

</PRE>


<P>

As with the matching operator, the regexp quote can use different
delimiters, e.g., <TT>&quot;qr!!&quot;</TT>, <TT>&quot;qr{}&quot;</TT> or <TT>&quot;qr~~&quot;</TT>.  Apostrophes
as delimiters (<TT>&quot;qr''&quot;</TT>) inhibit any interpolation.
<P>

Pre-compiled regexps are useful for creating dynamic matches that
don't need to be recompiled each time they are encountered.  Using
pre-compiled regexps, we write a <TT>&quot;grep_step&quot;</TT> program which greps
for a sequence of patterns, advancing to the next pattern as soon
as one has been satisfied.
<P>



<PRE>
    % cat &gt; grep_step
    #!/usr/bin/perl
    # grep_step - match &lt;number&gt; regexps, one after the other
    # usage: multi_grep &lt;number&gt; regexp1 regexp2 ... file1 file2 ...

    $number = shift;
    $regexp[$_] = shift foreach (0..$number-1);
    @compiled = map qr/$_/, @regexp;
    while ($line = &lt;&gt;) {
        if ($line =~ /$compiled[0]/) {
            print $line;
            shift @compiled;
            last unless @compiled;
        }
    }
    ^D

    % grep_step 3 shift print last grep_step
    $number = shift;
            print $line;
            last unless @compiled;

</PRE>


<P>

Storing pre-compiled regexps in an array <TT>@compiled</TT> allows us to
simply loop through the regexps without any recompilation, thus gaining
flexibility without sacrificing speed.
<A NAME="lbAW">&nbsp;</A>
<H3>Composing regular expressions at runtime</H3>

<A NAME="ixABQ"></A>
Backtracking is more efficient than repeated tries with different regular
expressions.  If there are several regular expressions and a match with
any of them is acceptable, then it is possible to combine them into a set
of alternatives.  If the individual expressions are input data, this
can be done by programming a join operation.  We'll exploit this idea in
an improved version of the <TT>&quot;simple_grep&quot;</TT> program: a program that matches
multiple patterns:
<P>



<PRE>
    % cat &gt; multi_grep
    #!/usr/bin/perl
    # multi_grep - match any of &lt;number&gt; regexps
    # usage: multi_grep &lt;number&gt; regexp1 regexp2 ... file1 file2 ...

    $number = shift;
    $regexp[$_] = shift foreach (0..$number-1);
    $pattern = join '|', @regexp;

    while ($line = &lt;&gt;) {
        print $line if $line =~ /$pattern/o;
    }
    ^D

    % multi_grep 2 shift for multi_grep
    $number = shift;
    $regexp[$_] = shift foreach (0..$number-1);

</PRE>


<P>

Sometimes it is advantageous to construct a pattern from the <I>input</I>
that is to be analyzed and use the permissible values on the left
hand side of the matching operations.  As an example for this somewhat
paradoxical situation, let's assume that our input contains a command
verb which should match one out of a set of available command verbs,
with the additional twist that commands may be abbreviated as long as
the given string is unique. The program below demonstrates the basic
algorithm.
<P>



<PRE>
    % cat &gt; keymatch
    #!/usr/bin/perl
    $kwds = 'copy compare list print';
    while( $command = &lt;&gt; ){
        $command =~ s/^\s+|\s+$//g;  # trim leading and trailing spaces
        if( ( @matches = $kwds =~ /\b$command\w*/g ) == 1 ){
            print &quot;command: '@matches'\n&quot;;
        } elsif( @matches == 0 ){
            print &quot;no such command: '$command'\n&quot;;
        } else {
            print &quot;not unique: '$command' (could be one of: @matches)\n&quot;;
        }
    }
    ^D

    % keymatch
    li
    command: 'list'
    co
    not unique: 'co' (could be one of: copy compare)
    printer
    no such command: 'printer'

</PRE>


<P>

Rather than trying to match the input against the keywords, we match the
combined set of keywords against the input.  The pattern matching
operation <TT>&quot;$kwds&nbsp;=~&nbsp;/\b($command\w*)/g&quot;</TT> does several things at the
same time. It makes sure that the given command begins where a keyword
begins (<TT>&quot;\b&quot;</TT>). It tolerates abbreviations due to the added <TT>&quot;\w*&quot;</TT>. It
tells us the number of matches (<TT>&quot;scalar @matches&quot;</TT>) and all the keywords
that were actually matched.  You could hardly ask for more.
<A NAME="lbAX">&nbsp;</A>
<H3>Embedding comments and modifiers in a regular expression</H3>

<A NAME="ixABR"></A>
Starting with this section, we will be discussing Perl's set of
<I>extended patterns</I>.  These are extensions to the traditional regular
expression syntax that provide powerful new tools for pattern
matching.  We have already seen extensions in the form of the minimal
matching constructs <TT>&quot;??&quot;</TT>, <TT>&quot;*?&quot;</TT>, <TT>&quot;+?&quot;</TT>, <TT>&quot;{n,m}?&quot;</TT>, and <TT>&quot;{n,}?&quot;</TT>.  The
rest of the extensions below have the form <TT>&quot;(?char...)&quot;</TT>, where the
<TT>&quot;char&quot;</TT> is a character that determines the type of extension.
<P>

The first extension is an embedded comment <TT>&quot;(?#text)&quot;</TT>.  This embeds a
comment into the regular expression without affecting its meaning.  The
comment should not have any closing parentheses in the text.  An
example is
<P>



<PRE>
    /(?# Match an integer:)[+-]?\d+/;

</PRE>


<P>

This style of commenting has been largely superseded by the raw,
freeform commenting that is allowed with the <TT>&quot;//x&quot;</TT> modifier.
<P>

The modifiers <TT>&quot;//i&quot;</TT>, <TT>&quot;//m&quot;</TT>, <TT>&quot;//s&quot;</TT> and <TT>&quot;//x&quot;</TT> (or any
combination thereof) can also be embedded in
a regexp using <TT>&quot;(?i)&quot;</TT>, <TT>&quot;(?m)&quot;</TT>, <TT>&quot;(?s)&quot;</TT>, and <TT>&quot;(?x)&quot;</TT>.  For instance,
<P>



<PRE>
    /(?i)yes/;  # match 'yes' case insensitively
    /yes/i;     # same thing
    /(?x)(          # freeform version of an integer regexp
             [+-]?  # match an optional sign
             \d+    # match a sequence of digits
         )
    /x;

</PRE>


<P>

Embedded modifiers can have two important advantages over the usual
modifiers.  Embedded modifiers allow a custom set of modifiers to
<I>each</I> regexp pattern.  This is great for matching an array of regexps
that must have different modifiers:
<P>



<PRE>
    $pattern[0] = '(?i)doctor';
    $pattern[1] = 'Johnson';
    ...
    while (&lt;&gt;) {
        foreach $patt (@pattern) {
            print if /$patt/;
        }
    }

</PRE>


<P>

The second advantage is that embedded modifiers (except <TT>&quot;//p&quot;</TT>, which
modifies the entire regexp) only affect the regexp
inside the group the embedded modifier is contained in.  So grouping
can be used to localize the modifier's effects:
<P>



<PRE>
    /Answer: ((?i)yes)/;  # matches 'Answer: yes', 'Answer: YES', etc.

</PRE>


<P>

Embedded modifiers can also turn off any modifiers already present
by using, e.g., <TT>&quot;(?-i)&quot;</TT>.  Modifiers can also be combined into
a single expression, e.g., <TT>&quot;(?s-i)&quot;</TT> turns on single line mode and
turns off case insensitivity.
<P>

Embedded modifiers may also be added to a non-capturing grouping.
<TT>&quot;(?i-m:regexp)&quot;</TT> is a non-capturing grouping that matches <TT>&quot;regexp&quot;</TT>
case insensitively and turns off multi-line mode.
<A NAME="lbAY">&nbsp;</A>
<H3>Looking ahead and looking behind</H3>

<A NAME="ixABS"></A>
This section concerns the lookahead and lookbehind assertions.  First,
a little background.
<P>

In Perl regular expressions, most regexp elements 'eat up' a certain
amount of string when they match.  For instance, the regexp element
<TT>&quot;[abc}]&quot;</TT> eats up one character of the string when it matches, in the
sense that Perl moves to the next character position in the string
after the match.  There are some elements, however, that don't eat up
characters (advance the character position) if they match.  The examples
we have seen so far are the anchors.  The anchor <TT>&quot;^&quot;</TT> matches the
beginning of the line, but doesn't eat any characters.  Similarly, the
word boundary anchor <TT>&quot;\b&quot;</TT> matches wherever a character matching <TT>&quot;\w&quot;</TT>
is next to a character that doesn't, but it doesn't eat up any
characters itself.  Anchors are examples of <I>zero-width assertions</I>.
Zero-width, because they consume
no characters, and assertions, because they test some property of the
string.  In the context of our walk in the woods analogy to regexp
matching, most regexp elements move us along a trail, but anchors have
us stop a moment and check our surroundings.  If the local environment
checks out, we can proceed forward.  But if the local environment
doesn't satisfy us, we must backtrack.
<P>

Checking the environment entails either looking ahead on the trail,
looking behind, or both.  <TT>&quot;^&quot;</TT> looks behind, to see that there are no
characters before.  <TT>&quot;$&quot;</TT> looks ahead, to see that there are no
characters after.  <TT>&quot;\b&quot;</TT> looks both ahead and behind, to see if the
characters on either side differ in their ``word-ness''.
<P>

The lookahead and lookbehind assertions are generalizations of the
anchor concept.  Lookahead and lookbehind are zero-width assertions
that let us specify which characters we want to test for.  The
lookahead assertion is denoted by <TT>&quot;(?=regexp)&quot;</TT> and the lookbehind
assertion is denoted by <TT>&quot;(?&lt;=fixed-regexp)&quot;</TT>.  Some examples are
<P>



<PRE>
    $x = &quot;I catch the housecat 'Tom-cat' with catnip&quot;;
    $x =~ /cat(?=\s)/;   # matches 'cat' in 'housecat'
    @catwords = ($x =~ /(?&lt;=\s)cat\w+/g);  # matches,
                                           # $catwords[0] = 'catch'
                                           # $catwords[1] = 'catnip'
    $x =~ /\bcat\b/;  # matches 'cat' in 'Tom-cat'
    $x =~ /(?&lt;=\s)cat(?=\s)/; # doesn't match; no isolated 'cat' in
                              # middle of $x

</PRE>


<P>

Note that the parentheses in <TT>&quot;(?=regexp)&quot;</TT> and <TT>&quot;(?&lt;=regexp)&quot;</TT> are
non-capturing, since these are zero-width assertions.  Thus in the
second regexp, the substrings captured are those of the whole regexp
itself.  Lookahead <TT>&quot;(?=regexp)&quot;</TT> can match arbitrary regexps, but
lookbehind <TT>&quot;(?&lt;=fixed-regexp)&quot;</TT> only works for regexps of fixed
width, i.e., a fixed number of characters long.  Thus
<TT>&quot;(?&lt;=(ab|bc))&quot;</TT> is fine, but <TT>&quot;(?&lt;=(ab)*)&quot;</TT> is not.  The
negated versions of the lookahead and lookbehind assertions are
denoted by <TT>&quot;(?!regexp)&quot;</TT> and <TT>&quot;(?&lt;!fixed-regexp)&quot;</TT> respectively.
They evaluate true if the regexps do <I>not</I> match:
<P>



<PRE>
    $x = &quot;foobar&quot;;
    $x =~ /foo(?!bar)/;  # doesn't match, 'bar' follows 'foo'
    $x =~ /foo(?!baz)/;  # matches, 'baz' doesn't follow 'foo'
    $x =~ /(?&lt;!\s)foo/;  # matches, there is no \s before 'foo'

</PRE>


<P>

The <TT>&quot;\C&quot;</TT> is unsupported in lookbehind, because the already
treacherous definition of <TT>&quot;\C&quot;</TT> would become even more so
when going backwards.
<P>

Here is an example where a string containing blank-separated words,
numbers and single dashes is to be split into its components.
Using <TT>&quot;/\s+/&quot;</TT> alone won't work, because spaces are not required between
dashes, or a word or a dash. Additional places for a split are established
by looking ahead and behind:
<P>



<PRE>
    $str = &quot;one two - --6-8&quot;;
    @toks = split / \s+              # a run of spaces
                  | (?&lt;=\S) (?=-)    # any non-space followed by '-'
                  | (?&lt;=-)  (?=\S)   # a '-' followed by any non-space
                  /x, $str;          # @toks = qw(one two - - - 6 - 8)

</PRE>


<A NAME="lbAZ">&nbsp;</A>
<H3>Using independent subexpressions to prevent backtracking</H3>

<A NAME="ixABT"></A>
<I>Independent subexpressions</I> are regular expressions, in the
context of a larger regular expression, that function independently of
the larger regular expression.  That is, they consume as much or as
little of the string as they wish without regard for the ability of
the larger regexp to match.  Independent subexpressions are represented
by <TT>&quot;(?&gt;regexp)&quot;</TT>.  We can illustrate their behavior by first
considering an ordinary regexp:
<P>



<PRE>
    $x = &quot;ab&quot;;
    $x =~ /a*ab/;  # matches

</PRE>


<P>

This obviously matches, but in the process of matching, the
subexpression <TT>&quot;a*&quot;</TT> first grabbed the <TT>&quot;a&quot;</TT>.  Doing so, however,
wouldn't allow the whole regexp to match, so after backtracking, <TT>&quot;a*&quot;</TT>
eventually gave back the <TT>&quot;a&quot;</TT> and matched the empty string.  Here, what
<TT>&quot;a*&quot;</TT> matched was <I>dependent</I> on what the rest of the regexp matched.
<P>

Contrast that with an independent subexpression:
<P>



<PRE>
    $x =~ /(?&gt;a*)ab/;  # doesn't match!

</PRE>


<P>

The independent subexpression <TT>&quot;(?&gt;a*)&quot;</TT> doesn't care about the rest
of the regexp, so it sees an <TT>&quot;a&quot;</TT> and grabs it.  Then the rest of the
regexp <TT>&quot;ab&quot;</TT> cannot match.  Because <TT>&quot;(?&gt;a*)&quot;</TT> is independent, there
is no backtracking and the independent subexpression does not give
up its <TT>&quot;a&quot;</TT>.  Thus the match of the regexp as a whole fails.  A similar
behavior occurs with completely independent regexps:
<P>



<PRE>
    $x = &quot;ab&quot;;
    $x =~ /a*/g;   # matches, eats an 'a'
    $x =~ /\Gab/g; # doesn't match, no 'a' available

</PRE>


<P>

Here <TT>&quot;//g&quot;</TT> and <TT>&quot;\G&quot;</TT> create a 'tag team' handoff of the string from
one regexp to the other.  Regexps with an independent subexpression are
much like this, with a handoff of the string to the independent
subexpression, and a handoff of the string back to the enclosing
regexp.
<P>

The ability of an independent subexpression to prevent backtracking
can be quite useful.  Suppose we want to match a non-empty string
enclosed in parentheses up to two levels deep.  Then the following
regexp matches:
<P>



<PRE>
    $x = &quot;abc(de(fg)h&quot;;  # unbalanced parentheses
    $x =~ /\( ( [^()]+ | \([^()]*\) )+ \)/x;

</PRE>


<P>

The regexp matches an open parenthesis, one or more copies of an
alternation, and a close parenthesis.  The alternation is two-way, with
the first alternative <TT>&quot;[^()]+&quot;</TT> matching a substring with no
parentheses and the second alternative <TT>&quot;\([^()]*\)&quot;</TT>  matching a
substring delimited by parentheses.  The problem with this regexp is
that it is pathological: it has nested indeterminate quantifiers
of the form <TT>&quot;(a+|b)+&quot;</TT>.  We discussed in Part 1 how nested quantifiers
like this could take an exponentially long time to execute if there
was no match possible.  To prevent the exponential blowup, we need to
prevent useless backtracking at some point.  This can be done by
enclosing the inner quantifier as an independent subexpression:
<P>



<PRE>
    $x =~ /\( ( (?&gt;[^()]+) | \([^()]*\) )+ \)/x;

</PRE>


<P>

Here, <TT>&quot;(?&gt;[^()]+)&quot;</TT> breaks the degeneracy of string partitioning
by gobbling up as much of the string as possible and keeping it.   Then
match failures fail much more quickly.
<A NAME="lbBA">&nbsp;</A>
<H3>Conditional expressions</H3>

<A NAME="ixABU"></A>
A <I>conditional expression</I> is a form of if-then-else statement
that allows one to choose which patterns are to be matched, based on
some condition.  There are two types of conditional expression:
<TT>&quot;(?(condition)yes-regexp)&quot;</TT> and
<TT>&quot;(?(condition)yes-regexp|no-regexp)&quot;</TT>.  <TT>&quot;(?(condition)yes-regexp)&quot;</TT> is
like an <TT>'if&nbsp;()&nbsp;{}'</TT> statement in Perl.  If the <TT>&quot;condition&quot;</TT> is true,
the <TT>&quot;yes-regexp&quot;</TT> will be matched.  If the <TT>&quot;condition&quot;</TT> is false, the
<TT>&quot;yes-regexp&quot;</TT> will be skipped and Perl will move onto the next regexp
element.  The second form is like an <TT>'if&nbsp;()&nbsp;{}&nbsp;else&nbsp;{}'</TT> statement
in Perl.  If the <TT>&quot;condition&quot;</TT> is true, the <TT>&quot;yes-regexp&quot;</TT> will be
matched, otherwise the <TT>&quot;no-regexp&quot;</TT> will be matched.
<P>

The <TT>&quot;condition&quot;</TT> can have several forms.  The first form is simply an
integer in parentheses <TT>&quot;(integer)&quot;</TT>.  It is true if the corresponding
backreference <TT>&quot;\integer&quot;</TT> matched earlier in the regexp.  The same
thing can be done with a name associated with a capture buffer, written
as <TT>&quot;(&lt;name&gt;)&quot;</TT> or <TT>&quot;('name')&quot;</TT>.  The second form is a bare
zero width assertion <TT>&quot;(?...)&quot;</TT>, either a lookahead, a lookbehind, or a
code assertion (discussed in the next section).  The third set of forms
provides tests that return true if the expression is executed within
a recursion (<TT>&quot;(R)&quot;</TT>) or is being called from some capturing group,
referenced either by number (<TT>&quot;(R1)&quot;</TT>, <TT>&quot;(R2)&quot;</TT>,...) or by name
(<TT>&quot;(R&amp;name)&quot;</TT>).
<P>

The integer or name form of the <TT>&quot;condition&quot;</TT> allows us to choose,
with more flexibility, what to match based on what matched earlier in the
regexp. This searches for words of the form <TT>&quot;$x$x&quot;</TT> or <TT>&quot;$x$y$y$x&quot;</TT>:
<P>



<PRE>
    % simple_grep '^(\w+)(\w+)?(?(2)\2\1|\1)$' /usr/dict/words
    beriberi
    coco
    couscous
    deed
    ...
    toot
    toto
    tutu

</PRE>


<P>

The lookbehind <TT>&quot;condition&quot;</TT> allows, along with backreferences,
an earlier part of the match to influence a later part of the
match.  For instance,
<P>



<PRE>
    /[ATGC]+(?(?&lt;=AA)G|C)$/;

</PRE>


<P>

matches a <FONT SIZE="-1">DNA</FONT> sequence such that it either ends in <TT>&quot;AAG&quot;</TT>, or some
other base pair combination and <TT>&quot;C&quot;</TT>.  Note that the form is
<TT>&quot;(?(?&lt;=AA)G|C)&quot;</TT> and not <TT>&quot;(?((?&lt;=AA))G|C)&quot;</TT>; for the
lookahead, lookbehind or code assertions, the parentheses around the
conditional are not needed.
<A NAME="lbBB">&nbsp;</A>
<H3>Defining named patterns</H3>

<A NAME="ixABV"></A>
Some regular expressions use identical subpatterns in several places.
Starting with Perl 5.10, it is possible to define named subpatterns in
a section of the pattern so that they can be called up by name
anywhere in the pattern.  This syntactic pattern for this definition
group is <TT>&quot;(?(DEFINE)(?&lt;name&gt;pattern)...)&quot;</TT>.  An insertion
of a named pattern is written as <TT>&quot;(?&amp;name)&quot;</TT>.
<P>

The example below illustrates this feature using the pattern for
floating point numbers that was presented earlier on.  The three
subpatterns that are used more than once are the optional sign, the
digit sequence for an integer and the decimal fraction.  The <FONT SIZE="-1">DEFINE</FONT>
group at the end of the pattern contains their definition.  Notice
that the decimal fraction pattern is the first place where we can
reuse the integer pattern.
<P>



<PRE>
   /^ (?&amp;osg)\ * ( (?&amp;int)(?&amp;dec)? | (?&amp;dec) )
      (?: [eE](?&amp;osg)(?&amp;int) )?
    $
    (?(DEFINE)
      (?&lt;osg&gt;[-+]?)         # optional sign
      (?&lt;int&gt;\d++)          # integer
      (?&lt;dec&gt;\.(?&amp;int))     # decimal fraction
    )/x

</PRE>


<A NAME="lbBC">&nbsp;</A>
<H3>Recursive patterns</H3>

<A NAME="ixABW"></A>
This feature (introduced in Perl 5.10) significantly extends the
power of Perl's pattern matching.  By referring to some other
capture group anywhere in the pattern with the construct
<TT>&quot;(?group-ref)&quot;</TT>, the <I>pattern</I> within the referenced group is used
as an independent subpattern in place of the group reference itself.
Because the group reference may be contained <I>within</I> the group it
refers to, it is now possible to apply pattern matching to tasks that
hitherto required a recursive parser.
<P>

To illustrate this feature, we'll design a pattern that matches if
a string contains a palindrome. (This is a word or a sentence that,
while ignoring spaces, interpunctuation and case, reads the same backwards
as forwards. We begin by observing that the empty string or a string
containing just one word character is a palindrome. Otherwise it must
have a word character up front and the same at its end, with another
palindrome in between.
<P>



<PRE>
    /(?: (\w) (?...Here be a palindrome...) \g{-1} | \w? )/x

</PRE>


<P>

Adding <TT>&quot;\W*&quot;</TT> at either end to eliminate what is to be ignored, we already
have the full pattern:
<P>



<PRE>
    my $pp = qr/^(\W* (?: (\w) (?1) \g{-1} | \w? ) \W*)$/ix;
    for $s ( &quot;saippuakauppias&quot;, &quot;A man, a plan, a canal: Panama!&quot; ){
        print &quot;'$s' is a palindrome\n&quot; if $s =~ /$pp/;
    }

</PRE>


<P>

In <TT>&quot;(?...)&quot;</TT> both absolute and relative backreferences may be used.
The entire pattern can be reinserted with <TT>&quot;(?R)&quot;</TT> or <TT>&quot;(?0)&quot;</TT>.
If you prefer to name your buffers, you can use <TT>&quot;(?&amp;name)&quot;</TT> to
recurse into that buffer.
<A NAME="lbBD">&nbsp;</A>
<H3>A bit of magic: executing Perl code in a regular expression</H3>

<A NAME="ixABX"></A>
Normally, regexps are a part of Perl expressions.
<I>Code evaluation</I> expressions turn that around by allowing
arbitrary Perl code to be a part of a regexp.  A code evaluation
expression is denoted <TT>&quot;(?{code})&quot;</TT>, with <I>code</I> a string of Perl
statements.
<P>

Be warned that this feature is considered experimental, and may be
changed without notice.
<P>

Code expressions are zero-width assertions, and the value they return
depends on their environment.  There are two possibilities: either the
code expression is used as a conditional in a conditional expression
<TT>&quot;(?(condition)...)&quot;</TT>, or it is not.  If the code expression is a
conditional, the code is evaluated and the result (i.e., the result of
the last statement) is used to determine truth or falsehood.  If the
code expression is not used as a conditional, the assertion always
evaluates true and the result is put into the special variable
<TT>$^R</TT>.  The variable <TT>$^R</TT> can then be used in code expressions later
in the regexp.  Here are some silly examples:
<P>



<PRE>
    $x = &quot;abcdef&quot;;
    $x =~ /abc(?{print &quot;Hi Mom!&quot;;})def/; # matches,
                                         # prints 'Hi Mom!'
    $x =~ /aaa(?{print &quot;Hi Mom!&quot;;})def/; # doesn't match,
                                         # no 'Hi Mom!'

</PRE>


<P>

Pay careful attention to the next example:
<P>



<PRE>
    $x =~ /abc(?{print &quot;Hi Mom!&quot;;})ddd/; # doesn't match,
                                         # no 'Hi Mom!'
                                         # but why not?

</PRE>


<P>

At first glance, you'd think that it shouldn't print, because obviously
the <TT>&quot;ddd&quot;</TT> isn't going to match the target string. But look at this
example:
<P>



<PRE>
    $x =~ /abc(?{print &quot;Hi Mom!&quot;;})[d]dd/; # doesn't match,
                                           # but _does_ print

</PRE>


<P>

Hmm. What happened here? If you've been following along, you know that
the above pattern should be effectively the same as the last one ---
enclosing the d in a character class isn't going to change what it
matches. So why does the first not print while the second one does?
<P>

The answer lies in the optimizations the regex engine makes. In the first
case, all the engine sees are plain old characters (aside from the
<TT>&quot;?{}&quot;</TT> construct). It's smart enough to realize that the string 'ddd'
doesn't occur in our target string before actually running the pattern
through. But in the second case, we've tricked it into thinking that our
pattern is more complicated than it is. It takes a look, sees our
character class, and decides that it will have to actually run the
pattern to determine whether or not it matches, and in the process of
running it hits the print statement before it discovers that we don't
have a match.
<P>

To take a closer look at how the engine does optimizations, see the
section ``Pragmas and debugging'' below.
<P>

More fun with <TT>&quot;?{}&quot;</TT>:
<P>



<PRE>
    $x =~ /(?{print &quot;Hi Mom!&quot;;})/;       # matches,
                                         # prints 'Hi Mom!'
    $x =~ /(?{$c = 1;})(?{print &quot;$c&quot;;})/;  # matches,
                                           # prints '1'
    $x =~ /(?{$c = 1;})(?{print &quot;$^R&quot;;})/; # matches,
                                           # prints '1'

</PRE>


<P>

The bit of magic mentioned in the section title occurs when the regexp
backtracks in the process of searching for a match.  If the regexp
backtracks over a code expression and if the variables used within are
localized using <TT>&quot;local&quot;</TT>, the changes in the variables produced by the
code expression are undone! Thus, if we wanted to count how many times
a character got matched inside a group, we could use, e.g.,
<P>



<PRE>
    $x = &quot;aaaa&quot;;
    $count = 0;  # initialize 'a' count
    $c = &quot;bob&quot;;  # test if $c gets clobbered
    $x =~ /(?{local $c = 0;})         # initialize count
           ( a                        # match 'a'
             (?{local $c = $c + 1;})  # increment count
           )*                         # do this any number of times,
           aa                         # but match 'aa' at the end
           (?{$count = $c;})          # copy local $c var into $count
          /x;
    print &quot;'a' count is $count, \$c variable is '$c'\n&quot;;

</PRE>


<P>

This prints
<P>



<PRE>
    'a' count is 2, $c variable is 'bob'

</PRE>


<P>

If we replace the <TT>&quot;&nbsp;(?{local&nbsp;$c&nbsp;=&nbsp;$c&nbsp;+&nbsp;1;})&quot;</TT> with
<TT>&quot;&nbsp;(?{$c&nbsp;=&nbsp;$c&nbsp;+&nbsp;1;})&quot;</TT>, the variable changes are <I>not</I> undone
during backtracking, and we get
<P>



<PRE>
    'a' count is 4, $c variable is 'bob'

</PRE>


<P>

Note that only localized variable changes are undone.  Other side
effects of code expression execution are permanent.  Thus
<P>



<PRE>
    $x = &quot;aaaa&quot;;
    $x =~ /(a(?{print &quot;Yow\n&quot;;}))*aa/;

</PRE>


<P>

produces
<P>



<PRE>
   Yow
   Yow
   Yow
   Yow

</PRE>


<P>

The result <TT>$^R</TT> is automatically localized, so that it will behave
properly in the presence of backtracking.
<P>

This example uses a code expression in a conditional to match a
definite article, either 'the' in English or 'der|die|das' in German:
<P>



<PRE>
    $lang = 'DE';  # use German
    ...
    $text = &quot;das&quot;;
    print &quot;matched\n&quot;
        if $text =~ /(?(?{
                          $lang eq 'EN'; # is the language English?
                         })
                       the |             # if so, then match 'the'
                       (der|die|das)     # else, match 'der|die|das'
                     )
                    /xi;

</PRE>


<P>

Note that the syntax here is <TT>&quot;(?(?{...})yes-regexp|no-regexp)&quot;</TT>, not
<TT>&quot;(?((?{...}))yes-regexp|no-regexp)&quot;</TT>.  In other words, in the case of a
code expression, we don't need the extra parentheses around the
conditional.
<P>

If you try to use code expressions with interpolating variables, Perl
may surprise you:
<P>



<PRE>
    $bar = 5;
    $pat = '(?{ 1 })';
    /foo(?{ $bar })bar/; # compiles ok, $bar not interpolated
    /foo(?{ 1 })$bar/;   # compile error!
    /foo${pat}bar/;      # compile error!

    $pat = qr/(?{ $foo = 1 })/;  # precompile code regexp
    /foo${pat}bar/;      # compiles ok

</PRE>


<P>

If a regexp has (1) code expressions and interpolating variables, or
(2) a variable that interpolates a code expression, Perl treats the
regexp as an error. If the code expression is precompiled into a
variable, however, interpolating is ok. The question is, why is this
an error?
<P>

The reason is that variable interpolation and code expressions
together pose a security risk.  The combination is dangerous because
many programmers who write search engines often take user input and
plug it directly into a regexp:
<P>



<PRE>
    $regexp = &lt;&gt;;       # read user-supplied regexp
    $chomp $regexp;     # get rid of possible newline
    $text =~ /$regexp/; # search $text for the $regexp

</PRE>


<P>

If the <TT>$regexp</TT> variable contains a code expression, the user could
then execute arbitrary Perl code.  For instance, some joker could
search for <TT>&quot;system('rm&nbsp;-rf&nbsp;*');&quot;</TT> to erase your files.  In this
sense, the combination of interpolation and code expressions <I>taints</I>
your regexp.  So by default, using both interpolation and code
expressions in the same regexp is not allowed.  If you're not
concerned about malicious users, it is possible to bypass this
security check by invoking <TT>&quot;use&nbsp;re&nbsp;'eval'&quot;</TT>:
<P>



<PRE>
    use re 'eval';       # throw caution out the door
    $bar = 5;
    $pat = '(?{ 1 })';
    /foo(?{ 1 })$bar/;   # compiles ok
    /foo${pat}bar/;      # compiles ok

</PRE>


<P>

Another form of code expression is the <I>pattern code expression</I>.
The pattern code expression is like a regular code expression, except
that the result of the code evaluation is treated as a regular
expression and matched immediately.  A simple example is
<P>



<PRE>
    $length = 5;
    $char = 'a';
    $x = 'aaaaabb';
    $x =~ /(??{$char x $length})/x; # matches, there are 5 of 'a'

</PRE>


<P>

This final example contains both ordinary and pattern code
expressions.  It detects whether a binary string <TT>1101010010001...</TT> has a
Fibonacci spacing 0,1,1,2,3,5,...  of the <TT>1</TT>'s:
<P>



<PRE>
    $x = &quot;1101010010001000001&quot;;
    $z0 = ''; $z1 = '0';   # initial conditions
    print &quot;It is a Fibonacci sequence\n&quot;
        if $x =~ /^1         # match an initial '1'
                    (?:
                       ((??{ $z0 })) # match some '0'
                       1             # and then a '1'
                       (?{ $z0 = $z1; $z1 .= $^N; })
                    )+   # repeat as needed
                  $      # that is all there is
                 /x;
    printf &quot;Largest sequence matched was %d\n&quot;, length($z1)-length($z0);

</PRE>


<P>

Remember that <TT>$^N</TT> is set to whatever was matched by the last
completed capture group. This prints
<P>



<PRE>
    It is a Fibonacci sequence
    Largest sequence matched was 5

</PRE>


<P>

Ha! Try that with your garden variety regexp package...
<P>

Note that the variables <TT>$z0</TT> and <TT>$z1</TT> are not substituted when the
regexp is compiled, as happens for ordinary variables outside a code
expression.  Rather, the code expressions are evaluated when Perl
encounters them during the search for a match.
<P>

The regexp without the <TT>&quot;//x&quot;</TT> modifier is
<P>



<PRE>
    /^1(?:((??{ $z0 }))1(?{ $z0 = $z1; $z1 .= $^N; }))+$/

</PRE>


<P>

which shows that spaces are still possible in the code parts. Nevertheless,
when working with code and conditional expressions, the extended form of
regexps is almost necessary in creating and debugging regexps.
<A NAME="lbBE">&nbsp;</A>
<H3>Backtracking control verbs</H3>

<A NAME="ixABY"></A>
Perl 5.10 introduced a number of control verbs intended to provide
detailed control over the backtracking process, by directly influencing
the regexp engine and by providing monitoring techniques.  As all
the features in this group are experimental and subject to change or
removal in a future version of Perl, the interested reader is
referred to ``Special Backtracking Control Verbs'' in perlre for a
detailed description.
<P>

Below is just one example, illustrating the control verb <TT>&quot;(*FAIL)&quot;</TT>,
which may be abbreviated as <TT>&quot;(*F)&quot;</TT>. If this is inserted in a regexp
it will cause to fail, just like at some mismatch between the pattern
and the string. Processing of the regexp continues like after any ``normal''
failure, so that, for instance, the next position in the string or another
alternative will be tried. As failing to match doesn't preserve capture
buffers or produce results, it may be necessary to use this in
combination with embedded code.
<P>



<PRE>
   %count = ();
   &quot;supercalifragilisticexpialidoceous&quot; =~
       /([aeiou])(?{ $count{$1}++; })(*FAIL)/oi;
   printf &quot;%3d '%s'\n&quot;, $count{$_}, $_ for (sort keys %count);

</PRE>


<P>

The pattern begins with a class matching a subset of letters.  Whenever
this matches, a statement like <TT>&quot;$count{'a'}++;&quot;</TT> is executed, incrementing
the letter's counter. Then <TT>&quot;(*FAIL)&quot;</TT> does what it says, and
the regexp  engine proceeds according to the book: as long as the end of
the string  hasn't been reached, the position is advanced before looking
for another vowel. Thus, match or no match makes no difference, and the
regexp engine proceeds until the the entire string has been inspected.
(It's remarkable that an alternative solution using something like
<P>



<PRE>
   $count{lc($_)}++ for split('', &quot;supercalifragilisticexpialidoceous&quot;);
   printf &quot;%3d '%s'\n&quot;, $count2{$_}, $_ for ( qw{ a e i o u } );

</PRE>


<P>

is considerably slower.)
<A NAME="lbBF">&nbsp;</A>
<H3>Pragmas and debugging</H3>

<A NAME="ixABZ"></A>
Speaking of debugging, there are several pragmas available to control
and debug regexps in Perl.  We have already encountered one pragma in
the previous section, <TT>&quot;use&nbsp;re&nbsp;'eval';&quot;</TT>, that allows variable
interpolation and code expressions to coexist in a regexp.  The other
pragmas are
<P>



<PRE>
    use re 'taint';
    $tainted = &lt;&gt;;
    @parts = ($tainted =~ /(\w+)\s+(\w+)/; # @parts is now tainted

</PRE>


<P>

The <TT>&quot;taint&quot;</TT> pragma causes any substrings from a match with a tainted
variable to be tainted as well.  This is not normally the case, as
regexps are often used to extract the safe bits from a tainted
variable.  Use <TT>&quot;taint&quot;</TT> when you are not extracting safe bits, but are
performing some other processing.  Both <TT>&quot;taint&quot;</TT> and <TT>&quot;eval&quot;</TT> pragmas
are lexically scoped, which means they are in effect only until
the end of the block enclosing the pragmas.
<P>



<PRE>
    use re 'debug';
    /^(.*)$/s;       # output debugging info

    use re 'debugcolor';
    /^(.*)$/s;       # output debugging info in living color

</PRE>


<P>

The global <TT>&quot;debug&quot;</TT> and <TT>&quot;debugcolor&quot;</TT> pragmas allow one to get
detailed debugging info about regexp compilation and
execution.  <TT>&quot;debugcolor&quot;</TT> is the same as debug, except the debugging
information is displayed in color on terminals that can display
termcap color sequences.  Here is example output:
<P>



<PRE>
    % perl -e 'use re &quot;debug&quot;; &quot;abc&quot; =~ /a*b+c/;'
    Compiling REx `a*b+c'
    size 9 first at 1
       1: <A HREF="/manpages/index.html?4+STAR">STAR</A>(4)
       2:   EXACT &lt;a&gt;(0)
       4: <A HREF="/manpages/index.html?7+PLUS">PLUS</A>(7)
       5:   EXACT &lt;b&gt;(0)
       7: EXACT &lt;c&gt;(9)
       9: END(0)
    floating `bc' at 0..2147483647 (checking floating) minlen 2
    Guessing start of match, REx `a*b+c' against `abc'...
    Found floating substr `bc' at offset 1...
    Guessed: match at offset 0
    Matching REx `a*b+c' against `abc'
      Setting an EVAL scope, savestack=3
       0 &lt;&gt; &lt;abc&gt;             |  1:  STAR
                               EXACT &lt;a&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       1 &lt;a&gt; &lt;bc&gt;             |  4:    PLUS
                               EXACT &lt;b&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       2 &lt;ab&gt; &lt;c&gt;             |  7:      EXACT &lt;c&gt;
       3 &lt;abc&gt; &lt;&gt;             |  9:      END
    Match successful!
    Freeing REx: `a*b+c'

</PRE>


<P>

If you have gotten this far into the tutorial, you can probably guess
what the different parts of the debugging output tell you.  The first
part
<P>



<PRE>
    Compiling REx `a*b+c'
    size 9 first at 1
       1: <A HREF="/manpages/index.html?4+STAR">STAR</A>(4)
       2:   EXACT &lt;a&gt;(0)
       4: <A HREF="/manpages/index.html?7+PLUS">PLUS</A>(7)
       5:   EXACT &lt;b&gt;(0)
       7: EXACT &lt;c&gt;(9)
       9: END(0)

</PRE>


<P>

describes the compilation stage.  <TT><A HREF="/manpages/index.html?4+STAR">STAR</A>(4)</TT> means that there is a
starred object, in this case <TT>'a'</TT>, and if it matches, goto line 4,
i.e., <TT><A HREF="/manpages/index.html?7+PLUS">PLUS</A>(7)</TT>.  The middle lines describe some heuristics and
optimizations performed before a match:
<P>



<PRE>
    floating `bc' at 0..2147483647 (checking floating) minlen 2
    Guessing start of match, REx `a*b+c' against `abc'...
    Found floating substr `bc' at offset 1...
    Guessed: match at offset 0

</PRE>


<P>

Then the match is executed and the remaining lines describe the
process:
<P>



<PRE>
    Matching REx `a*b+c' against `abc'
      Setting an EVAL scope, savestack=3
       0 &lt;&gt; &lt;abc&gt;             |  1:  STAR
                               EXACT &lt;a&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       1 &lt;a&gt; &lt;bc&gt;             |  4:    PLUS
                               EXACT &lt;b&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       2 &lt;ab&gt; &lt;c&gt;             |  7:      EXACT &lt;c&gt;
       3 &lt;abc&gt; &lt;&gt;             |  9:      END
    Match successful!
    Freeing REx: `a*b+c'

</PRE>


<P>

Each step is of the form <TT>&quot;n&nbsp;&lt;x&gt;&nbsp;&lt;y&gt;&quot;</TT>, with <TT>&quot;&lt;x&gt;&quot;</TT> the
part of the string matched and <TT>&quot;&lt;y&gt;&quot;</TT> the part not yet
matched.  The <TT>&quot;|&nbsp;&nbsp;1:&nbsp;&nbsp;STAR&quot;</TT> says that Perl is at line number 1
n the compilation list above.  See
``Debugging regular expressions'' in perldebguts for much more detail.
<P>

An alternative method of debugging regexps is to embed <TT>&quot;print&quot;</TT>
statements within the regexp.  This provides a blow-by-blow account of
the backtracking in an alternation:
<P>



<PRE>
    &quot;that this&quot; =~ m@(?{print &quot;Start at position &quot;, pos, &quot;\n&quot;;})
                     t(?{print &quot;t1\n&quot;;})
                     h(?{print &quot;h1\n&quot;;})
                     i(?{print &quot;i1\n&quot;;})
                     s(?{print &quot;s1\n&quot;;})
                         |
                     t(?{print &quot;t2\n&quot;;})
                     h(?{print &quot;h2\n&quot;;})
                     a(?{print &quot;a2\n&quot;;})
                     t(?{print &quot;t2\n&quot;;})
                     (?{print &quot;Done at position &quot;, pos, &quot;\n&quot;;})
                    @x;

</PRE>


<P>

prints
<P>



<PRE>
    Start at position 0
    t1
    h1
    t2
    h2
    a2
    t2
    Done at position 4

</PRE>


<A NAME="lbBG">&nbsp;</A>
<H2>BUGS</H2>

<A NAME="ixACA"></A>
Code expressions, conditional expressions, and independent expressions
are <I>experimental</I>.  Don't use them in production code.  Yet.
<A NAME="lbBH">&nbsp;</A>
<H2>SEE ALSO</H2>

<A NAME="ixACB"></A>
This is just a tutorial.  For the full story on Perl regular
expressions, see the perlre regular expressions reference page.
<P>

For more information on the matching <TT>&quot;m//&quot;</TT> and substitution <TT>&quot;s///&quot;</TT>
operators, see ``Regexp Quote-Like Operators'' in perlop.  For
information on the <TT>&quot;split&quot;</TT> operation, see ``split'' in perlfunc.
<P>

For an excellent all-around resource on the care and feeding of
regular expressions, see the book <I>Mastering Regular Expressions</I> by
Jeffrey Friedl (published by O'Reilly, <FONT SIZE="-1">ISBN</FONT> 1556592-257-3).
<A NAME="lbBI">&nbsp;</A>
<H2>AUTHOR AND COPYRIGHT</H2>

<A NAME="ixACC"></A>
Copyright (c) 2000 Mark Kvale
All rights reserved.
<P>

This document may be distributed under the same terms as Perl itself.
<A NAME="lbBJ">&nbsp;</A>
<H3>Acknowledgments</H3>

<A NAME="ixACD"></A>
The inspiration for the stop codon <FONT SIZE="-1">DNA</FONT> example came from the <FONT SIZE="-1">ZIP</FONT>
code example in chapter 7 of <I>Mastering Regular Expressions</I>.
<P>

The author would like to thank Jeff Pinyan, Andrew Johnson, Peter
Haworth, Ronald J Kimball, and Joe Smith for all their helpful
comments.
<P>

<HR>
<A NAME="index">&nbsp;</A><H2>Index</H2>
<DL>
<DT><A HREF="#lbAB">NAME</A><DD>
<DT><A HREF="#lbAC">DESCRIPTION</A><DD>
<DT><A HREF="#lbAD">Part 1: The basics</A><DD>
<DL>
<DT><A HREF="#lbAE">Simple word matching</A><DD>
<DT><A HREF="#lbAF">Using character classes</A><DD>
<DT><A HREF="#lbAG">Matching this or that</A><DD>
<DT><A HREF="#lbAH">Grouping things and hierarchical matching</A><DD>
<DT><A HREF="#lbAI">Extracting matches</A><DD>
<DT><A HREF="#lbAJ">Backreferences</A><DD>
<DT><A HREF="#lbAK">Relative backreferences</A><DD>
<DT><A HREF="#lbAL">Named backreferences</A><DD>
<DT><A HREF="#lbAM">Alternative capture group numbering</A><DD>
<DT><A HREF="#lbAN">Position information</A><DD>
<DT><A HREF="#lbAO">Non-capturing groupings</A><DD>
<DT><A HREF="#lbAP">Matching repetitions</A><DD>
<DT><A HREF="#lbAQ">Possessive quantifiers</A><DD>
<DT><A HREF="#lbAR">Building a regexp</A><DD>
<DT><A HREF="#lbAS">Using regular expressions in Perl</A><DD>
</DL>
<DT><A HREF="#lbAT">Part 2: Power tools</A><DD>
<DL>
<DT><A HREF="#lbAU">More on characters, strings, and character classes</A><DD>
<DT><A HREF="#lbAV">Compiling and saving regular expressions</A><DD>
<DT><A HREF="#lbAW">Composing regular expressions at runtime</A><DD>
<DT><A HREF="#lbAX">Embedding comments and modifiers in a regular expression</A><DD>
<DT><A HREF="#lbAY">Looking ahead and looking behind</A><DD>
<DT><A HREF="#lbAZ">Using independent subexpressions to prevent backtracking</A><DD>
<DT><A HREF="#lbBA">Conditional expressions</A><DD>
<DT><A HREF="#lbBB">Defining named patterns</A><DD>
<DT><A HREF="#lbBC">Recursive patterns</A><DD>
<DT><A HREF="#lbBD">A bit of magic: executing Perl code in a regular expression</A><DD>
<DT><A HREF="#lbBE">Backtracking control verbs</A><DD>
<DT><A HREF="#lbBF">Pragmas and debugging</A><DD>
</DL>
<DT><A HREF="#lbBG">BUGS</A><DD>
<DT><A HREF="#lbBH">SEE ALSO</A><DD>
<DT><A HREF="#lbBI">AUTHOR AND COPYRIGHT</A><DD>
<DL>
<DT><A HREF="#lbBJ">Acknowledgments</A><DD>
</DL>
</DL>
<HR>
This document was created by
<A HREF="/manpages/index.html">man2html</A>,
using the manual pages.<BR>
Time: 05:29:08 GMT, December 24, 2015
</div></div>
</body>
</HTML>
